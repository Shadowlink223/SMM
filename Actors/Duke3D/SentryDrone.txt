ACTOR SentryDrone
{
	Health 150
	Radius 16
	Height 30
	Speed  10
	PainChance 256
	Scale 0.65
	MONSTER
	+FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOTDMATCH
	+NOICEDEATH
	+THRUGHOST
	+DONTGIB
	+NOTARGET
	+NOINFIGHTING
	+DONTMORPH
	+NOBLOOD
	SeeSound "sentdrone/sight"
	PainSound "sentdrone/pain"
	DeathSound "sentdrone/detonate"
	ActiveSound "sentdrone/active"
	Dropitem "DefaultPinataPop"
	Obituary "A Sentry Drone delivered its payload to %o"
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	PainChance "MarathonStun", 255
	States
	{
		Spawn:
			SEDR A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_PlaySound ("sentdrone/active", 6, 1.0, 1, 2)
		See2:
			SEDR A 1 A_Wander
			SEDR A 1 A_Chase
			Loop
		Pain:
			TNT1 A 0 A_FastChase
			TNT1 A 0 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			SEDR A 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			SEDR A 30 A_Pain
			SEDR A 70
			Goto See
		Missile:
			TNT1 A 0 A_Recoil (3)
			SEDR AAAAAAAAAAAAAAA 2 A_FaceTarget
			TNT1 A 0 A_PlaySound ("sentdrone/charge") 
			SEDR A 4 A_SkullAttack (10)
			SEDR A 1
			Goto Missile+18
		Melee:
			TNT1 A 0 A_FaceTarget
			SEDR A 2 A_Die
			Goto Death
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SEDR A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			SEDR A 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			SEDR A 3 A_SetTranslucent(0.9,0)
			SEDR A 0 A_Pain
			SEDR A 3 A_SetTranslucent(0.8,0)
			SEDR A 0 A_NoBlocking
			SEDR A 3 A_SetTranslucent(0.7,0)
			SEDR A 3 A_SetTranslucent(0.6,0)
			SEDR A 3 A_SetTranslucent(0.5,0)
			SEDR A 3 A_SetTranslucent(0.4,0)
			SEDR A 3 A_SetTranslucent(0.3,0)
			SEDR A 3 A_SetTranslucent(0.2,0)
			SEDR A 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A 0 A_StopSound(6)
			TNT1 A -1
			stop
		Death:
			TNT1 A 0 A_NoBlocking
			TNT1 A 5 A_SpawnItemEx("SentryDroneBomb",0,0,32,0,0,0,0,1,0)
			TNT1 A 0 A_PlaySound ("sentdrone/detonate", CHAN_AUTO)
			stop
	}
}

ACTOR SentryDroneBomb
{
	Radius 1
	Height 1
	Speed 0
	+NOCLIP
	PROJECTILE
	States
	{
		Spawn:
			TNT1 A 0
			Goto Death
		Death:
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
			TNT1 AAAAAAAA 0 A_SpawnDebris("DukeShrapnel1")
			TNT1 AAAA 0 A_SpawnDebris("DukeShrapnel2")
			TNT1 AAAAAAA 0 A_SpawnDebris("DukeShrapnel3")
			TNT1 A 0 A_Explode(35, 142, 1)
			stop
  }
}

ACTOR MultiSentryDrone : SentryDrone
{
	Species "SentryDrone"
	+THRUSPECIES
}