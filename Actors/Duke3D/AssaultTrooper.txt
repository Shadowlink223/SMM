ACTOR AssaultTrooper
{
	Health 30
	Radius 18
	Height 51
	Speed 20
	PainChance 96
	Scale 0.65
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
	+NOINFIGHTING
    +THRUGHOST
	+MISSILEMORE
	SeeSound "assaultenf/sight"
	PainSound "assaultenf/pain"
	DeathSound "assaultenf/death"
	ActiveSound "assaultcapt/roam"
	AttackSound ""
	Obituary "An Assault Trooper killed %o with his laser."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	Decal BulletChip
	States 
	{
		Spawn:
			ASTR A 1 A_Look
			Loop
		See:
			ASTR ABCD 5 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump (64, "CrouchShot")
			Goto StandShot
		StandShot:
			ASTR EE 9 A_Facetarget 
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			TNT1 A 0 Bright A_CustomMissile("TrooperLaser",32,10,0,8)
			ASTR EE 9 A_Facetarget 
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			TNT1 A 0 Bright A_CustomMissile("TrooperLaser",32,10,0,8)
			Goto See
		CrouchShot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			ASTR GG 9 A_FaceTarget
			ASTR HH 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			TNT1 A 0 Bright A_CustomMissile("TrooperLaser",32,10,0,8)
			ASTR GG 4 A_FaceTarget
			ASTR HH 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			TNT1 A 0 Bright A_CustomMissile("TrooperLaser",32,10,0,8)
			ASTR GG 4 A_FaceTarget
			ASTR HH 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			TNT1 A 0 Bright A_CustomMissile("TrooperLaser",32,10,0,8)
			ASTR GG 4 A_FaceTarget
			ASTR HH 4 A_FaceTarget
			Goto See
		Pain:
			ASTR I 3
			ASTR I 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			ASTR I 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			ASTR I 16 A_Pain
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			goto XDeath
		Death.Explosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"XDeath")
		Death.FireExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"XDeath")
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			ASTR I 7 A_Scream
			ASTR J 5 A_NoBlocking
			ASTR K 6
			ASTR L 6 A_SpawnItemEx("ATrooperAmmoDrop",0,0,0,0,0,10,0,0,160)
			ASTR M -1
			stop
		XDeath:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			Stop
		Death.ZorchPistol:
		Death.Zorch:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			ASTR I 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			ASTR I 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			ASTR I 3 A_SetTranslucent(0.9,0)
			ASTR I 0 A_Scream
			ASTR I 3 A_SetTranslucent(0.8,0)
			ASTR I 0 A_NoBlocking
			ASTR I 3 A_SetTranslucent(0.7,0)
			ASTR I 3 A_SetTranslucent(0.6,0)
			ASTR I 3 A_SetTranslucent(0.5,0)
			ASTR I 3 A_SetTranslucent(0.4,0)
			ASTR I 3 A_SetTranslucent(0.3,0)
			ASTR I 3 A_SetTranslucent(0.2,0)
			ASTR I 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			ASTR O 0 A_GenericFreezeDeath
			ASTR O 70 A_UnsetSolid
			ASTR O 0 A_IceGuyDie
			ASTR O 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			ASTR MLKJI 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			ASTR A 3 A_SetTranslucent(0.2,0)
			ASTR A 3 A_SetTranslucent(0.3,0)
			ASTR A 3 A_SetTranslucent(0.4,0)
			ASTR A 3 A_SetTranslucent(0.5,0)
			ASTR A 3 A_SetTranslucent(0.6,0)
			ASTR A 3 A_SetTranslucent(0.7,0)
			ASTR A 3 A_SetTranslucent(0.8,0)
			ASTR A 3 A_SetTranslucent(0.9,0)
			ASTR A 3 A_SetTranslucent(1,0)
			ASTR A 1 A_SetShootable	
			Goto See
	}
}

ACTOR InvisAssaultTrooper : AssaultTrooper
{
	States 
	{
		Spawn:
			TNT1 A 1 A_Look
			Loop
		See:
			TNT1 AAAA 5 A_Chase
			Loop
		Pain:
			TNT1 I 3
			TNT1 I 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 I 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			TNT1 I 16 A_Pain
			Goto See
	}
}