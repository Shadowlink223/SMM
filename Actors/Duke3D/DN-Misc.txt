actor DukeEnemyBullet : DoomEnemyBullet
{
	Damage (5)
	
	States
	{
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("DukeBulletPuff",0,0,0,0,0,0,0,32)
			stop
	}
}

ACTOR QueenAttackToken : CustomInventory { Inventory.MaxAmount 5 }

actor ACommanderRocket
{
	Radius 12
	Height 8
	Speed 30
	Projectile
	Obituary "%o was blown to bits by an Assault Commander"
	Scale 0.4
	Decal Scorch
	+FORCERADIUSDMG
	
	States
	{
		Spawn:
			RPGX A 1 bright
			loop
		
		Death:
		TNT1 A 0 A_Explode((35), 128)
		TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
		TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
		stop
		
	}
}

ACTOR EnforcerTurd
{
	Height 2
	Radius 2
	+NOINTERACTION
	States
	{
		Spawn:
			TURD A -1
			Stop
	}
}

ACTOR BattleLordMortar 
{
	Height 4
	Radius 2
	Speed 8
	damage (10)
	Gravity 0.6
	PROJECTILE
	+DOOMBOUNCE //+HERETICBOUNCE
	//+BOUNCEONFLOORS
	-NOGRAVITY
	+DONTFALL
	BounceFactor 0.9
	BounceCount 10
	SeeSound "duke/pipebomb"
    Decal Scorch
	Obituary "%o was mortared by the Battlelord."
	States 
	{
		Spawn:
			BLMR ABCD 2 A_SpawnItemEx("BattleLordMortarAfterImage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(128, 128)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEx("RPGExplosion")
			stop
	}
}

ACTOR BattleLordMortarAfterImage 
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			TNT1 A 0
			BLMR ABCD 2 A_FadeOut
			Loop
	}
}

ACTOR BattleLordChaingunToken : CustomInventory { Inventory.MaxAmount 12 }
ACTOR BattleLordMortarToken : CustomInventory { Inventory.MaxAmount 4 }
ACTOR BattleLordMortar1 : BattleLordMortar { Speed 26 }
ACTOR BattleLordMortar2 : BattleLordMortar { Speed 32 }
ACTOR BattleLordMortar3 : BattleLordMortar { Speed 38 }

ACTOR CycloidEmperorRocketToken : CustomInventory { Inventory.MaxAmount 5 }
ACTOR CycloidEmperorEnergyToken : CustomInventory { Inventory.MaxAmount 12 }
ACTOR CyEmperorRocket : ACommanderRocket { Obituary "The Cycloid Emperor's rockets took care of %o" }

ACTOR OctabrainProjectile 
{
	Radius 16
	Height 12
	Speed 20
	Damage (38)
	Scale 0.5
	Projectile
	+FORCERADIUSDMG
	Obituary "%o was brainshocked by an Octabrain"
	Decal Scorch
	States 
	{
		Spawn:
			OCPR ABCDEFGHIJKLMNOPQRST 3
			Goto Death
		Death:
			Stop
	}
}

actor OverlordRocket : ACommanderRocket { Obituary "%o was destroyed by the Overlord" }

actor PigCopBullet : DukeEnemyBullet { Damage (10) }
actor PigProneBullet : DukeEnemyBullet { Damage (6) }