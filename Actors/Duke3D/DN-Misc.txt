ACTOR DukeEnemyBullet : DoomEnemyBullet
{
	Damage (5)
	
	States
	{
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("DukeBulletPuff",0,0,0,0,0,0,0,32)
			stop
	}
}

ACTOR TrooperLaser
{
  Radius 13
  Height 8
  Speed 35
  Damage (7)
  PROJECTILE
  Decal bulletchip
  Scale 0.3
  States
  {
	Spawn:
		ASLS A 0 bright A_SpawnItemEx("TrooperLaserTrail",-20,0,0,0,0,0,0,128,0)
		ASLS A 0 bright A_SpawnItemEx("TrooperLaserTrail",-15,0,0,0,0,0,0,128,0)
		ASLS A 0 bright A_SpawnItemEx("TrooperLaserTrail",-10,0,0,0,0,0,0,128,0)
		ASLS A 1 bright A_SpawnItemEx("TrooperLaserTrail",-5,0,0,0,0,0,0,128,0)
		loop
	Death:
		TNT1 A 1
		Stop
  }
}

ACTOR TrooperLaserTrail
{
	Radius 0
	Height 0
	Speed 0
	Damage 0
	PROJECTILE
	scale 0.3
	States
	{
	  Spawn:
		ASLS A 2 bright
		stop
	}
}

ACTOR DukeTeleportFlash
{
	+NOINTERACTION
	Scale 0.75
	States
	{
	  Spawn:
			TFLA ABCD 1
			TFLA EF 2
			TFLA EDC 1
			TFLA B 2
			TFLA G 1
	  Death:
			TNT1 A 0
			Stop
	}
}

ACTOR QueenAttackToken : CustomInventory { Inventory.MaxAmount 5 }

ACTOR OverlordRocketPuff : DukeRocketPuff
{
	RenderStyle Translucent
	Alpha 0.5

	States
	{
      Spawn:
        TNT1 A 2 
        DKPF ABCD 4
        Stop
    }
}

ACTOR ACommanderRocket
{
	Radius 12
	Height 8
	Speed 30
	PROJECTILE
	Obituary "%o was blown to bits by an Assault Commander"
	Scale 0.4
	Decal Scorch
	+FORCERADIUSDMG
	
	States
	{
		Spawn:
			RPGX A 1 bright A_SpawnItemEx("DukeRocketPuff",0,0,0,0,0,0,0,160)
			loop
		
		Death:
			TNT1 A 0 A_Explode(35, 111)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
		stop
		
	}
}

ACTOR OverlordRocket : ACommanderRocket
{
	Radius 16
	Height 12
	Obituary "%o was destroyed by the Overlord"
	Scale 0.8
	
	States
	{
		Death:
			TNT1 A 0 A_Explode(140, 111)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
			stop
	}
}

ACTOR CyEmperorRocket : ACommanderRocket
{
	Speed 35
	Obituary "%o was dealt with by the Cycloid Emperor's rockets"
	
	States
	{
	
		Spawn:
			RPGX A 1 bright
			loop
		
		Death:
			TNT1 A 0 A_Explode(140, 111)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
			stop
	}
}

ACTOR EnforcerTurd
{
	Height 2
	Radius 2
	Scale 0.65
	+NOINTERACTION
	States
	{
		Spawn:
			TURD A -1
			Stop
	}
}

ACTOR NewDukeGibs1 replaces DukeGibs1
{
	Radius 1
    Height 1
    Speed 0
    Projectile
    -NOGRAVITY
    -SOLID
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    Gravity 0.65
    Health 2
    scale 0.65
	States
	{
		Spawn:
			DKG1 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEF 5
			Stop
		Death2:
			DKG1 E 2100
			Loop
	}
}

ACTOR NewDukeGibs2 : NewDukeGibs1 replaces DukeGibs2
{
	Health 2
	States
	{
		Spawn:
			DKG2 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEF 5
			Stop
		Death2:
			DKG2 E 2100
			Loop
	}
}

ACTOR NewDukeGibs3 : NewDukeGibs1 replaces DukeGibs3
{
	Health 4
	States
	{
		Spawn:
			DKG3 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEF 5
			Stop
		Death2:
			DKG3 E 2100
			Loop
	}
}

ACTOR NewDukeGibs4 : NewDukeGibs1 replaces DukeGibs4
{
	States
	{
		Spawn:
			DKG4 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEF 5
			Stop
		Death2:
			DKG4 E 2100
			Loop
	}
}

ACTOR NewDukeGibs5 :NewDukeGibs1 replaces DukeGibs5
{
	States
	{
		Spawn:
			DKG5 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEF 5
			Stop
		Death2:
			DKG5 E 2100
			Loop
	}
}

ACTOR NewDukeGibs6 : NewDukeGibs1 replaces DukeGibs6
{
	States
	{
		Spawn:
			DKG6 AB 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE,31) == 1, "Death2")
			DKG6 ABCDEFGH 5
			Stop
		Death2:
			DKG6 CDEFGH 5
			DKG6 H 2100
			Goto Death2+6
	}
}

Actor PigDrop : SamsaraSlotTwoDrop
{
	Scale 0.65
    States
    {
      Spawn:
        DSGP A -1
        stop
    }
}

ACTOR AEnforcerDrop : SamsaraSlotFourDrop
{
	Scale 0.65
	States
    {
      Spawn:
        DCCP A -1
        stop
    }
}

ACTOR AEnforcerAmmoDrop : Ammo1Big
{
	Scale 0.65
	States
    {
      Spawn:
        DCAP A -1
        stop
    }
}

ACTOR ATrooperAmmoDrop : Ammo1Small
{
	Scale 0.40
	States
    {
      Spawn:
        DPAP A -1
        stop
    }
}

ACTOR DukeArmor : BasicArmorPickup
{
  inventory.pickupmessage "GOT USED ARMOR"
  inventory.icon "DARPA0"
  Inventory.PickupSound "duke/kickhit"
  armor.saveamount 50
  armor.savepercent 35
  Scale 0.65
  states
  {
  Spawn:
    DARP A 6
    loop
  }
}

ACTOR RandomPigDrop : RandomSpawner
{
	dropitem "PigDrop" 255 4
	dropitem "DukeArmor" 255 2
}

ACTOR RandomEnforcerDrop : RandomSpawner
{
	dropitem "AEnforcerAmmoDrop" 255 4
	dropitem "AEnforcerDrop" 255 2
}

ACTOR BattleLordMortar 
{
	Height 4
	Radius 2
	Speed 8
	damage (10)
	Gravity 0.6
	PROJECTILE
	+DOOMBOUNCE //+HERETICBOUNCE
	//+BOUNCEONFLOORS
	-NOGRAVITY
	+DONTFALL
	BounceFactor 0.9
	BounceCount 10
	SeeSound "duke/pipebomb"
    Decal Scorch
	Obituary "%o was mortared by the Battlelord."
	States 
	{
		Spawn:
			BLMR ABCD 2 A_SpawnItemEx("BattleLordMortarAfterImage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(50, 156)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEx("RPGExplosion")
			stop
	}
}

ACTOR BattleLordMortarAfterImage 
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			TNT1 A 0
			BLMR ABCD 2 A_FadeOut
			Loop
	}
}

ACTOR BattleLordChaingunToken : CustomInventory { Inventory.MaxAmount 12 }
ACTOR BattleLordMortarToken : CustomInventory { Inventory.MaxAmount 4 }
ACTOR BattleLordMortar1 : BattleLordMortar { Speed 26 }
ACTOR BattleLordMortar2 : BattleLordMortar { Speed 32 }
ACTOR BattleLordMortar3 : BattleLordMortar { Speed 38 }

ACTOR CycloidEmperorRocketToken : CustomInventory { Inventory.MaxAmount 5 }
ACTOR CycloidEmperorEnergyToken : CustomInventory { Inventory.MaxAmount 12 }

ACTOR TankMissileToken : Inventory { Inventory.MaxAmount 11 }

ACTOR OctabrainProjectile 
{
	Radius 16
	Height 12
	Speed 16
	Damage (38)
	Scale 0.5
	PROJECTILE
	+FORCERADIUSDMG
	Obituary "%o was brainshocked by an Octabrain"
	Decal Scorch
	States 
	{
		Spawn:
			TNT1 A 0 A_PlaySound("octabrain/attack", CHAN_AUTO)
			OCPR ABCDEFGHIJKLMNOPQRST 3
			Goto Death
		Death:
			Stop
	}
}

ACTOR PigCopBullet : DukeEnemyBullet { Damage (10) }
ACTOR PigProneBullet : DukeEnemyBullet { Damage (6) }