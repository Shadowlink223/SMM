ACTOR PigCop 
{
	Health 100
	Radius 20
	Height 51
	Speed 15
	PainChance 32
	Scale 0.65
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
	+NOINFIGHTING
    +THRUGHOST
	+MISSILEMORE
	SeeSound "pigcop/sight"
	PainSound "pigcop/pain"
	DeathSound "pigcop/death"
	ActiveSound "pigcop/roam"
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was shotgunned by a Pig Cop."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "DukeBlood"
	Decal BulletChip
	States 
	{
		Spawn:
			PIGC A 1 A_Look
			Loop
		See:
			PIGC ABCD 7 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump (64, "ProneShot")
			Goto StandShot
		StandShot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PIGC EE 5 A_FaceTarget
			PIGC E 3
			TNT1 A 0 A_PlaySound("duke/mossberg", CHAN_WEAPON)
			TNT1 AAAAAAA 0 Bright A_CustomMissile("PigCopBullet",32,0,random(-12,12),8,random(-12,12))
			PIGC F 15 Bright
			PIGC G 7 A_PlaySound("duke/mosspump2", 5)
			PIGC EE 5 A_FaceTarget
			PIGC E 7 
			TNT1 A 0 A_PlaySound("duke/mossberg", CHAN_WEAPON)
			TNT1 AAAAAAA 0 Bright A_CustomMissile("PigCopBullet",32,0,random(-12,12),8,random(-12,12))
			PIGC F 16 Bright
			PIGC G 7 A_PlaySound("duke/mosspump2", 5)
			PIGC E 9 A_CPosRefire
			Goto See
		ProneShot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PIGC H 11 A_FaceTarget
			PIGC IIIIII 5 A_FaceTarget
			TNT1 AAAAAAA 0 Bright A_CustomMissile("PigProneBullet",32,0,random(-12,12),8,random(-12,12))
			PIGC J 18 Bright A_PlaySound("duke/mossberg", CHAN_WEAPON)
			PIGC I 17 A_PlaySound("duke/mosspump2", 5)
			TNT1 AAAAAAA 0 Bright A_CustomMissile("PigProneBullet",32,0,random(-12,12),8,random(-12,12))
			PIGC J 16 Bright A_PlaySound("duke/mossberg", CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("duke/mosspump2", 5)
			TNT1 A 0 A_CPosRefire
			Goto See
		Pain:
			PIGC K 3
			PIGC K 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			PIGC K 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			PIGC K 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			PIGC K 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			PIGC K 30 A_Pain
			PIGC K 70
			Goto See
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
			Goto Death
		DukeGibStart:
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			goto XDeath
		Death.Explosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"XDeath")
			Goto Death
		Death.FireExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"XDeath")
			Goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			PIGC K 3 A_Scream
			PIGC L 5
			PIGC M 5 A_NoBlocking
			PIGC N 4 A_SpawnItemEx("RandomPigDrop",0,0,0,0,0,10,0,40,128)
			PIGC OP 5
			PIGC P -1
			stop
		XDeath:
			TNT1 A 1
			TNT1 A 1 A_UnsetSolid
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("DukeJibs1")
			TNT1 A 0 A_SpawnDebris("DukeJibs2")
			TNT1 AA 0 A_SpawnDebris("DukeJibs3")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs4")
			TNT1 AA 0 A_SpawnDebris("DukeJibs5")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			PIGC K 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			PIGC K 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			PIGC K 3 A_SetTranslucent(0.9,0)
			PIGC K 0 A_Scream
			PIGC K 3 A_SetTranslucent(0.8,0)
			PIGC K 0 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("RandomPigDrop",0,0,0,0,0,10,0,40,128)
			PIGC K 3 A_SetTranslucent(0.7,0)
			PIGC K 3 A_SetTranslucent(0.6,0)
			PIGC K 3 A_SetTranslucent(0.5,0)
			PIGC K 3 A_SetTranslucent(0.4,0)
			PIGC K 3 A_SetTranslucent(0.3,0)
			PIGC K 3 A_SetTranslucent(0.2,0)
			PIGC K 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			PIGC K 0 A_GenericFreezeDeath
			PIGC K 70 A_UnsetSolid
			PIGC K 0 A_IceGuyDie
			PIGC K 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			PIGC PONMLK 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			PIGC A 3 A_SetTranslucent(0.2,0)
			PIGC A 3 A_SetTranslucent(0.3,0)
			PIGC A 3 A_SetTranslucent(0.4,0)
			PIGC A 3 A_SetTranslucent(0.5,0)
			PIGC A 3 A_SetTranslucent(0.6,0)
			PIGC A 3 A_SetTranslucent(0.7,0)
			PIGC A 3 A_SetTranslucent(0.8,0)
			PIGC A 3 A_SetTranslucent(0.9,0)
			PIGC A 3 A_SetTranslucent(1,0)
			PIGC A 1 A_SetShootable	
			Goto See
	}
}

ACTOR InvisPigCop : PigCop
{
	States 
	{
		Spawn:
			TNT1 A 1 A_Look
			Loop
		See:
			TNT1 AAAA 7 A_Chase
			Loop
		Pain:
			TNT1 A 3
			TNT1 A 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			TNT1 A 16 A_Pain
			Goto See
	}
}

ACTOR MultiPigCop : PigCop
{
	Species "PigCop"
	+THRUSPECIES
}