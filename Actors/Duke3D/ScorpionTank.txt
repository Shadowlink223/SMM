ACTOR ScorpionTank
{
	Health 500
	Radius 32
	Height 64
	Mass 1000
	Speed 10
	PainChance 0
	Scale 0.75
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+NOBLOOD
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	AttackSound "duke/m1911"
	Dropitem "DefaultPinataPop"
	Obituary "%o was shredded by a Pig Cop Tank."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	PainChance "MarathonStun", 255
	Decal BulletChip
	States 
	{
		Spawn:
			PIGS A 0
			PIGS A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PIGS A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			PIGS A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PIGS A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_PlaySound("pigtank/active", CHAN_VOICE)
			PIGS AAAAAAAAAAAAAAA 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIfInventory("TankMissileToken", 10, "MissileEnd")
			TNT1 A 0 A_GiveInventory ("TankMissileToken",1)
			PIGS A 2 A_FaceTarget(22)
			TNT1 A 0 A_PlaySound("assaultcapt/fire", CHAN_WEAPON)
			PIGS A 0 Bright A_CustomMissile("TrooperLaser",24,22,-2,CMF_CHECKTARGETDEAD,0)
			PIGS A 6 Bright A_CustomMissile("TrooperLaser",24,-22,2,CMF_CHECKTARGETDEAD,0)
			Loop
		MissileEnd:
			TNT1 A 0 A_TakeInventory ("TankMissileToken", 11)
			TNT1 A 0 A_JumpIfCloser (1024, 3)
			TNT1 A 0 A_PlaySound("duke/rocketfire", CHAN_WEAPON)
			PIGS A 0 Bright A_CustomMissile("ACommanderRocket",56,0,0,CMF_CHECKTARGETDEAD,0)
			PIGS A 40
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			PIGS A 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			PIGS A 30 A_Pain
			PIGS A 70
			Goto See
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
		Death.DukeIce:
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,160)
			TNT1 A 1 A_SpawnItemEx("PigCop",0,0,0,0,0,0,0,0,127)
			TNT1 A -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			PIGS A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			PIGS A 4 A_SpawnItem("ZorchEffectBig")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			PIGS A 4 A_SetTranslucent(0.9,0)
			PIGS A 4 A_SetTranslucent(0.8,0)
			PIGS A 0 A_NoBlocking
			PIGS A 4 A_SetTranslucent(0.7,0)
			PIGS A 4 A_SetTranslucent(0.6,0)
			PIGS A 4 A_SetTranslucent(0.5,0)
			PIGS A 4 A_SetTranslucent(0.4,0)
			PIGS A 4 A_SetTranslucent(0.3,0)
			PIGS A 4 A_SetTranslucent(0.2,0)
			PIGS A 4 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
	}
}

ACTOR MultiScorpionTank : ScorpionTank
{
	Species "ScorpionTank"
	+THRUSPECIES
}