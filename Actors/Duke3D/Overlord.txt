ACTOR Overlord 
{
	Health 4500
	Radius 50
	Height 110
	Mass 1200
	Speed 35
	PainChance 32
	Scale 1.3
	MONSTER
	+BOSS
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+NOINFIGHTING
    +THRUGHOST
	+MISSILEMORE
	SeeSound ""
	PainSound "overlord/pain"
	DeathSound "overlord/death"
	ActiveSound "overlord/roam"
	AttackSound ""
	Obituary "%o was stomped on by the Overlord."
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "DragonClaw", 1.6
    DamageFactor "MarathonFusion", 2.0
    DamageFactor "WMCNade", 0.75
	DamageFactor "CorvusHellstaffTomed", 0.2
	States 
	{
		Spawn:
			OVRL A 2 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TNT1 A 0 A_GiveInventory("DukeBossSee",1)
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
		See2:
			OVRL AABBCCDD 5 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIfCloser(100,"Stomp")
			OVRL EEEEFFFFGGGG 3 A_FaceTarget
			Goto Spam
		Spam:
			OVRL GGG 2 A_FaceTarget
			OVRL H 4 Bright A_CustomMissile("OverlordRocket",100,15,0,8)
			OVRL H 2 A_FaceTarget
			OVRL GGG 2 A_FaceTarget
			OVRL H 4 Bright A_CustomMissile("OverlordRocket",100,-15,0,8)
			OVRL H 1 A_Jump (60, "See2")
			Goto Spam
		Stomp:
			TNT1 A 0 A_Explode(1000,100,0,0)
			Goto See2
		Pain:
			TNT1 A 0 A_Pain
			Goto See2
		Death.DukeDamage:
		Death.DukePistol:
		Death.DukeIce:
		Death.MightyBoot:
		Death.DukeExplosive:
			TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			OVRL I 8 A_Scream
			OVRL JKLMNOPQR 8 A_NoBlocking
			OVRL R 22
			OVRL R -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			OVRL I 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorchhuge","soundslot5",0,0)
			OVRL I 8 A_SpawnItem("ZorchEffectHuge")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			OVRL I 8 A_SetTranslucent(0.9,0)
			OVRL I 0 A_Scream
			OVRL I 8 A_SetTranslucent(0.8,0)
			OVRL I 0 A_NoBlocking
			OVRL I 8 A_SetTranslucent(0.7,0)
			OVRL I 8 A_SetTranslucent(0.6,0)
			OVRL I 8 A_SetTranslucent(0.5,0)
			OVRL I 8 A_SetTranslucent(0.4,0)
			OVRL I 8 A_SetTranslucent(0.3,0)
			OVRL I 8 A_SetTranslucent(0.2,0)
			OVRL I 8 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A 30
			TNT1 A -1 A_KillMaster
			stop
	}
}

ACTOR OverlordDoom : Overlord
{
	Radius 40
	YScale 1.0
	XScale 0.75
}