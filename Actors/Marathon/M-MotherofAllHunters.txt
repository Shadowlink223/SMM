ACTOR MotherofAllHunters
{
	Health 1500
	Radius 24
	Height 64
	Speed 4.629
	Mass 1000
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MinMissileChance 10
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+NOBLOOD
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	SeeSound ""
	PainSound ""
	DeathSound "hunter/crash"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o's spine became a Major Pfhor Hunter's trophy."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonFusion", 1.8
	DamageFactor "MarathonPlasmaFire", 0.74
	DamageFactor "Fire", 0.0
	DamageFactor "WolfenFlamethrower", 0.0
	DamageFactor "HexGreenFire", 0.0
	DamageFactor "FireExplosive", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.6
	States 
	{
		Spawn:
			HUMO A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			HUMO A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			HUMO A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			HUMO A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			HUMO A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17) //17.5
			Goto Spawn2
		Spawn2:
			HUMO A 12 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",6,"See2")
			//TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,2)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMO B 1 A_Chase
			Loop
			HUMO B 1 A_Chase(0,0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",12,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMO C 1 A_Chase
			Loop
			HUMO C 1 A_Chase(0,0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",18,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMO D 1 A_Chase
			Loop
			HUMO D 1 A_Chase(0,0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",24,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMO E 1 A_Chase
			Loop
			HUMO E 1 A_Chase(0,0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			Goto See
		Missile:
			HUMO A 3 A_FaceTarget
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",3,"MissileEnd")
			HUMO A 5 A_FaceTarget
			HUMO F 4 Bright A_CustomMissile("SeekerHunterShotITYTD", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileITYTD+2
		MissileHNTR:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",3,"MissileEnd")
			HUMO A 5 A_FaceTarget
			HUMO F 4 Bright A_CustomMissile("SeekerHunterShotHNTR", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHNTR+2
		MissileHMP:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",5,"MissileEnd")
			HUMO A 5 A_FaceTarget
			HUMO F 4 Bright A_CustomMissile("SeekerHunterShot", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHMP+2
		MissileUV:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",6,"MissileEnd")
			HUMO A 5 A_FaceTarget
			HUMO F 4 Bright A_CustomMissile("SeekerHunterShotUV", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileUV+2
		MissileNightmare:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("AttackReps",6,"MissileEnd")
			HUMO A 5 A_FaceTarget
			HUMO F 4 Bright A_CustomMissile("SeekerHunterShotNightmare", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileNightmare+2
		MissileEnd:
			TNT1 A 0 A_TakeInventory("AttackReps",6)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
		Pain:
			HUMO E 6
			TNT1 A 0 A_TakeInventory("AttackReps",6)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("AttackReps",6)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			HUMO E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			HUMO E 16
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			HUMO E 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			HUMO E 30 A_Pain
			HUMO E 70
			Goto See
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		DukeGibStart:
			HUMO K 1
			TNT1 A 0 A_UnsetSolid
			HUMO K 1 A_SpawnDebris("NewDukeGibs1_MajorHunter")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_MajorHunter")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs3_MajorHunter")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs4_MajorHunter")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs5_MajorHunter")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs6_MajorHunter")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
		Death.Explosive:
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMO G 5
			HUMO GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeath2")
			HUMO G 2 A_CheckFloor("XDeath2")
			Goto XDeath+3
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			TNT1 A 0 A_SpawnItemEx("MotherHunterExplosion",0,0,32,0,0,0,0,1,0)
			HUMO K 5 Bright A_PlaySoundEx("hunter/explode","body",0,0)
			HUMO L 4 Bright A_Stop
			HUMO MN 5 Bright A_NoBlocking
			HUMO O -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			HUMO E 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			HUMO E 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			HUMO E 3 A_SetTranslucent(0.9,0)
			HUMO E 0 A_Scream
			HUMO E 3 A_SetTranslucent(0.8,0)
			HUMO E 0 A_NoBlocking
			HUMO E 3 A_SetTranslucent(0.7,0)
			HUMO E 3 A_SetTranslucent(0.6,0)
			HUMO E 3 A_SetTranslucent(0.5,0)
			HUMO E 3 A_SetTranslucent(0.4,0)
			HUMO E 3 A_SetTranslucent(0.3,0)
			HUMO E 3 A_SetTranslucent(0.2,0)
			HUMO E 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			HUMO E 0 A_GenericFreezeDeath
			HUMO E 70 A_UnsetSolid
			HUMO E 0 A_IceGuyDie
			HUMO E 1 A_IceGuyDie
			TNT1 A -1 A_KillMaster
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			HUMO JIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			HUMO A 3 A_SetTranslucent(0.2,0)
			HUMO A 3 A_SetTranslucent(0.3,0)
			HUMO A 3 A_SetTranslucent(0.4,0)
			HUMO A 3 A_SetTranslucent(0.5,0)
			HUMO A 3 A_SetTranslucent(0.6,0)
			HUMO A 3 A_SetTranslucent(0.7,0)
			HUMO A 3 A_SetTranslucent(0.8,0)
			HUMO A 3 A_SetTranslucent(0.9,0)
			HUMO A 3 A_SetTranslucent(1,0)
			HUMO A 1 A_SetShootable
			Goto See
	}
}

ACTOR MotherofAllHuntersITYTD : MotherofAllHunters 
{ 
	Speed 4.073 
	Health 750 
}
ACTOR MotherofAllHuntersHNTR : MotherofAllHunters
{ 
	Speed 4.351 
	Health 1125 
}
ACTOR MotherofAllHuntersUV : MotherofAllHunters
{ 
	Speed 5.23
	Health 1875 
}
ACTOR MotherofAllHuntersNightmare : MotherofAllHunters
{ 
	Speed 5.786
	Health 2250
}

ACTOR MultiMotherofAllHunters : MotherofAllHunters
{
	Species "HunterMother"
	+THRUSPECIES
}
ACTOR MultiMotherofAllHuntersITYTD : MotherofAllHuntersITYTD
{
	Species "HunterMother"
	+THRUSPECIES
}
ACTOR MultiMotherofAllHuntersHNTR : MotherofAllHuntersHNTR
{
	Species "HunterMother"
	+THRUSPECIES
}
ACTOR MultiMotherofAllHuntersUV : MotherofAllHuntersUV
{
	Species "HunterMother"
	+THRUSPECIES
}
ACTOR MultiMotherofAllHuntersNightmare : MotherofAllHuntersNightmare
{
	Species "HunterMother"
	+THRUSPECIES
}