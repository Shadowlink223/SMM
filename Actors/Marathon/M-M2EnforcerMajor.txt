ACTOR M2EnforcerMajor
{
	Health 160
	Radius 16
	Height 56
	Speed 5.83
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	SeeSound "m2enforcer/sight"
	PainSound ""
	DeathSound "pfhor/death"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was burned to a crisp by a Major Enforcer."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonPlasmaFire", 0.74
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.49
	Bloodcolor Yellow
	States 
	{
		Spawn:
			ENMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			ENMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			ENMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			ENMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			ENMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17) //17.5
			Goto Spawn2
		Spawn2:
			ENMA A 3 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",8,"See2")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			ENMA B 1 A_Chase
			Loop
			ENMA B 1 A_Chase(0,0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",16,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			ENMA A 1 A_Chase
			Loop
			ENMA A 1 A_Chase(0,0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",24,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			ENMA C 1 A_Chase
			Loop
			ENMA C 1 A_Chase(0,0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",32,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			ENMA A 1 A_Chase
			Loop
			ENMA A 1 A_Chase(0,0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			Goto See
		Stand:
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			ENMA A 1 A_Chase(0,"Missile",CHF_DONTMOVE)
			Loop
			ENMA A 1 A_Chase(0,0,CHF_DONTMOVE)
			Loop
		Missile:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("EnforcerShotToken",6,"MissileEnd")
			ENMA E 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("m2enforcer/shoot", CHAN_WEAPON)
			ENMA F 3 Bright A_CustomMissile("EnforcerShotEnemyITYTD", 50, 0, Random(-4,4), 0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("EnforcerShotToken",1)
			Goto MissileITYTD+2
		MissileHNTR:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("EnforcerShotToken",6,"MissileEnd")
			ENMA E 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("m2enforcer/shoot", CHAN_WEAPON)
			ENMA F 3 Bright A_CustomMissile("EnforcerShotEnemyHNTR", 50, 0, Random(-4,4), 0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("EnforcerShotToken",1)
			Goto MissileHNTR+2
		MissileHMP:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("EnforcerShotToken",12,"MissileEnd")
			ENMA E 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("m2enforcer/shoot", CHAN_WEAPON)
			ENMA F 3 Bright A_CustomMissile("EnforcerShotEnemy", 50, 0, Random(-4,4), 0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("EnforcerShotToken",1)
			Goto MissileHMP+2
		MissileUV:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("EnforcerShotToken",13,"MissileEnd")
			ENMA E 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("m2enforcer/shoot", CHAN_WEAPON)
			ENMA F 3 Bright A_CustomMissile("EnforcerShotEnemyUV", 50, 0, Random(-4,4), 0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("EnforcerShotToken",1)
			Goto MissileUV+2
		MissileNightmare:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("EnforcerShotToken",13,"MissileEnd")
			ENMA E 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("m2enforcer/shoot", CHAN_WEAPON)
			ENMA F 3 Bright A_CustomMissile("EnforcerShotEnemyNightmare", 50, 0, Random(-4,4), 0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("EnforcerShotToken",1)
			Goto MissileNightmare+2
		MissileEnd:
			TNT1 A 0 A_TakeInventory("EnforcerShotToken",13)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto Stand
		Pain:
			ENMA A 1
			TNT1 A 0 A_TakeInventory("EnforcerShotToken",12)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("EnforcerShotToken",12)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			ENMA F 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			ENMA F 16
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			ENMA D 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			ENMA D 30 A_Pain
			ENMA D 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		DukeGibStart:
			ENMA L 1
			TNT1 A 0 A_UnsetSolid
			ENMA L 1 A_SpawnDebris("NewDukeGibs1_Pfhor")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_M2Enforcer")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs3_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs4_Pfhor")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs5_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs6_Pfhor")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Recoil(5)
			ENMA G 13
			ENMA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Death2")
			ENMA G 2 A_CheckFloor("Death2")
			Goto Death+4
		Death2:
			ENMA H 4 A_Stop
			ENMA I 5
			ENMA J 4 A_NoBlocking
			ENMA K 0 A_PlaySoundEx("marathon/splash","body",0,0)
			ENMA K 1 A_KillMaster
			ENMA K -1 A_SpawnItemEx("M2EnforcerDrop",0,0,0,0,0,0,0,32)
			Stop
		Death.Explosive:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Recoil(5)
			ENMA G 4
			ENMA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeath2")
			ENMA G 2 A_CheckFloor("XDeath2")
			Goto XDeath+4
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			ENMA L 5 A_PlaySoundEx("pfhor/xdeath","body",0,0)
			ENMA M 5 A_Stop
			ENMA N 5 
			ENMA O 5 A_NoBlocking
			ENMA P 5
			ENMA Q -1 A_KillMaster
			Stop
		Death.WolfenFlamethrower:
		Death.HexGreenFire:
		Death.QuakeFire:
		Death.FireExplosive:
		Death.MarathonPlasmaFire:
		Burn:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			SOPL U 13 Bright A_PlaySound("pfhor/wail")
			SOPL UUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUU 2 Bright A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Burn2")
			SOPL U 2 A_CheckFloor("Burn2")
			Goto Burn+3
		Burn2:
			SOPL V 5 Bright A_Stop
			SOPL W 4 Bright A_NoBlocking
			SOPL X 5 Bright
			SOPL X -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			ENMA D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			ENMA D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			ENMA D 3 A_SetTranslucent(0.9,0)
			ENMA D 0 A_Scream
			ENMA D 3 A_SetTranslucent(0.8,0)
			ENMA D 0 A_NoBlocking
			ENMA D 3 A_SetTranslucent(0.7,0)
			ENMA D 3 A_SetTranslucent(0.6,0)
			ENMA D 3 A_SetTranslucent(0.5,0)
			ENMA D 3 A_SetTranslucent(0.4,0)
			ENMA D 3 A_SetTranslucent(0.3,0)
			ENMA D 3 A_SetTranslucent(0.2,0)
			ENMA D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A 1 A_KillMaster
			TNT1 A -1 A_SpawnItemEx("M2EnforcerDrop",0,0,0,0,0,0,0,0,32)
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			ENMA D 0 A_GenericFreezeDeath
			ENMA D 70 A_UnsetSolid
			ENMA D 0 A_IceGuyDie
			ENMA D 1 A_IceGuyDie
			TNT1 A -1 A_KillMaster
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			ENMA KJIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			WGRD A 3 A_SetTranslucent(0.2,0)
			WGRD A 3 A_SetTranslucent(0.3,0)
			WGRD A 3 A_SetTranslucent(0.4,0)
			WGRD A 3 A_SetTranslucent(0.5,0)
			WGRD A 3 A_SetTranslucent(0.6,0)
			WGRD A 3 A_SetTranslucent(0.7,0)
			WGRD A 3 A_SetTranslucent(0.8,0)
			WGRD A 3 A_SetTranslucent(0.9,0)
			WGRD A 3 A_SetTranslucent(1,0)
			WGRD A 1 A_SetShootable
			Goto See
	}
}

ACTOR M2EnforcerMajorITYTD : M2EnforcerMajor 
{ 
	Speed 5.13 
	Health 80 
}
ACTOR M2EnforcerMajorHNTR : M2EnforcerMajor 
{ 
	Speed 5.48 
	Health 120 
}
ACTOR M2EnforcerMajorUV : M2EnforcerMajor 
{ 
	Speed 6.587
	Health 200 
}
ACTOR M2EnforcerMajorNightmare : M2EnforcerMajor 
{ 
	Speed 7.287 
	Health 240
}

ACTOR MultiM2EnforcerMajor : M2EnforcerMajor
{
	Species "M2EnforcerMajor"
	+THRUSPECIES
}
ACTOR MultiM2EnforcerMajorITYTD : M2EnforcerMajorITYTD
{
	Species "M2EnforcerMajor"
	+THRUSPECIES
}
ACTOR MultiM2EnforcerMajorHNTR : M2EnforcerMajorHNTR
{
	Species "M2EnforcerMajor"
	+THRUSPECIES
}
ACTOR MultiM2EnforcerMajorUV : M2EnforcerMajorUV
{
	Species "M2EnforcerMajor"
	+THRUSPECIES
}
ACTOR MultiM2EnforcerMajorNightmare : M2EnforcerMajorNightmare
{
	Species "M2EnforcerMajor"
	+THRUSPECIES
}