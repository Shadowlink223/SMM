ACTOR FighterMajor
{
	Health 80
	Radius 16
	Height 56
	Speed 4.049
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound "pfhorfighter/sight"
	PainSound ""
	DeathSound "pfhor/death"
	ActiveSound "pfhor/roam"
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was beaten to death by a Major Pfhor Fighter's staff."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonPlasmaFire", 0.74
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.49
	Bloodcolor Yellow
	States 
	{
		Spawn:
			FIMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			FIMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			FIMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			FIMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			FIMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			Goto Spawn2
		Spawn2:
			FIMA A 2 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",5,"See2")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			FIMA B 1 A_Chase
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",10,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			FIMA C 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",15,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			FIMA D 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",20,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			FIMA E 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",20)
			Goto See
		Melee:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,6)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MeleeITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MeleeHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MeleeHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MeleeUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MeleeNightmare")
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			FIMA F 1 A_FaceTarget
			FIMA F 1 A_JumpIfCloser(70,"Melee")
			Goto See
		MeleeITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto MissileEnd
		MeleeHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto MissileEnd
		MeleeHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto MissileEnd
		MeleeUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto MissileEnd
		MeleeNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			Goto MissileEnd
		MissileEnd:
			FIMA F 4 
			FIMA G 5 bright A_PlaySound("fighterstaff/swing")
			FIMA H 5 bright
			FIMA I 5 bright A_CustomMeleeAttack((random(20,25)),"fighterstaff/hit",0)
			Goto See
		Pain:
			FIMA J 5
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("HunterShotToken",12)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			FIMA J 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			FIMA J 16
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			Goto XDeathDuke
			/*TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathDuke")
			Goto XDeath*/
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			FIMA O 1 A_SpawnDebris("NewDukeGibs1_Pfhor")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_Pfhor")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs3_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs4_Pfhor")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs5_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs6_Pfhor")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(5)
			TNT1 A 0 A_Scream
			FIMA K 13
			FIMA KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Death2")
			FIMA K 2 A_CheckFloor("Death2")
			Goto Death+5
		Death2:
			FIMA L 7 A_Stop
			FIMA M 7 A_NoBlocking
			FIMA N 0 A_PlaySoundEx("marathon/splash","body",0,0)
			FIMA N -1
			Stop
		Death.Explosive:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(5)
			TNT1 A 0 A_Scream
			FIMA K 6
			FIMA KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeath2")
			FIMA K 2 A_CheckFloor("XDeath2")
			Goto XDeath+5
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			FIMA O 3 A_PlaySoundEx("pfhor/xdeath","body",0,0)
			FIMA P 3 A_Stop
			FIMA Q 3 A_NoBlocking
			FIMA R 3
			FIMA S -1
			Stop
		XDeathDuke:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(10)
			TNT1 A 0 A_Scream
			FIMA K 6
			FIMA KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeathDuke2")
			FIMA K 2 A_CheckFloor("XDeathDuke2")
			Goto XDeathDuke+5
		XDeathDuke2:
			PFDG A 1
			TNT1 A 0 A_UnsetSolid
			PFDG A 1 A_SpawnDebris("NewDukeGibs1_Pfhor")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_Pfhor")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs3_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs4_Pfhor")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs5_Pfhor")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs6_Pfhor")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			PFDG A 3 A_NoBlocking
			PFDG B 4 A_Stop
			PFDG CD 4
			PFDG E -1
			Stop
		Death.HexGreenFire:
		Death.QuakeFire:
		Death.FireExplosive:
		Death.MarathonPlasmaFire:
		Burn:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			SOPL U 13 Bright A_PlaySound("pfhor/wail")
			SOPL UUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUU 2 Bright A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Burn2")
			SOPL U 2 A_CheckFloor("Burn2")
			Goto Burn+3
		Burn2:
			SOPL V 5 Bright A_Stop
			SOPL W 4 Bright A_NoBlocking
			SOPL X 5 Bright
			SOPL X -1
			Stop
		Death.Crush:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_NoBlocking
			TNT1 A -1 A_PlaySound("pfhor/wail")
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			FIMA J 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			FIMA J 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			FIMA J 3 A_SetTranslucent(0.9,0)
			FIMA J 0 A_Scream
			FIMA J 3 A_SetTranslucent(0.8,0)
			FIMA J 0 A_NoBlocking
			FIMA J 3 A_SetTranslucent(0.7,0)
			FIMA J 3 A_SetTranslucent(0.6,0)
			FIMA J 3 A_SetTranslucent(0.5,0)
			FIMA J 3 A_SetTranslucent(0.4,0)
			FIMA J 3 A_SetTranslucent(0.3,0)
			FIMA J 3 A_SetTranslucent(0.2,0)
			FIMA J 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			FIMA J 0 A_GenericFreezeDeath
			FIMA J 70 A_UnsetSolid
			FIMA J 0 A_IceGuyDie
			FIMA J 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			FIMA NMLK 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			FIMA A 3 A_SetTranslucent(0.2,0)
			FIMA A 3 A_SetTranslucent(0.3,0)
			FIMA A 3 A_SetTranslucent(0.4,0)
			FIMA A 3 A_SetTranslucent(0.5,0)
			FIMA A 3 A_SetTranslucent(0.6,0)
			FIMA A 3 A_SetTranslucent(0.7,0)
			FIMA A 3 A_SetTranslucent(0.8,0)
			FIMA A 3 A_SetTranslucent(0.9,0)
			FIMA A 3 A_SetTranslucent(1,0)
			FIMA A 1 A_SetShootable
			Goto See
	}
}

ACTOR FighterMajorITYTD : FighterMajor
{
	Health 40
	Speed 3.563
}

ACTOR FighterMajorHNTR : FighterMajor
{
	Health 60
	Speed 3.806
}

ACTOR FighterMajorUV : FighterMajor
{
	Health 100
	Speed 4.575
}

ACTOR FighterMajorNightmare : FighterMajor
{
	Health 120
	Speed 5.061
}

ACTOR MultiFighterMajor : FighterMajor
{
	Species "FighterMajor"
	+THRUSPECIES
}
ACTOR MultiFighterMajorITYTD : FighterMajorITYTD
{
	Species "FighterMajor"
	+THRUSPECIES
}
ACTOR MultiFighterMajorHNTR : FighterMajorHNTR
{
	Species "FighterMajor"
	+THRUSPECIES
}
ACTOR MultiFighterMajorUV : FighterMajorUV
{
	Species "FighterMajor"
	+THRUSPECIES
}
ACTOR MultiFighterMajorNightmare : FighterMajorNightmare
{
	Species "FighterMajor"
	+THRUSPECIES
}