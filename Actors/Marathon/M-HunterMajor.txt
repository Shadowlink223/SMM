ACTOR HunterMajor
{
	Health 300
	Radius 24
	Height 64
	Speed 5.31
	Mass 1000
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MinMissileChance 10
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+NOBLOOD
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound ""
	PainSound ""
	DeathSound "hunter/crash"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o's spine became a Major Pfhor Hunter's trophy."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonFusion", 1.8
	DamageFactor "MarathonPlasmaFire", 0.74
	DamageFactor "Fire", 0.0
	DamageFactor "HexGreenFire", 0.0
	DamageFactor "FireExplosive", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.49
	States 
	{
		Spawn:
			HUMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			HUMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			HUMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			HUMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			HUMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17) //17.5
			Goto Spawn2
		Spawn2:
			HUMA A 12 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",6,"See2")
			//TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,2)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMA B 1 A_Chase
			Loop
			HUMA B 1 A_Chase(0,0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",12,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMA C 1 A_Chase
			Loop
			HUMA C 1 A_Chase(0,0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",18,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMA D 1 A_Chase
			Loop
			HUMA D 1 A_Chase(0,0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",24,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMA E 1 A_Chase
			Loop
			HUMA E 1 A_Chase(0,0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			Goto See
		Missile:
			HUMA A 3 A_FaceTarget
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",3,"MissileEnd")
			HUMA A 5 A_FaceTarget
			HUMA F 4 Bright A_CustomMissile("HunterShotITYTD", 60, 0, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileITYTD+2
		MissileHNTR:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",3,"MissileEnd")
			HUMA A 5 A_FaceTarget
			HUMA F 4 Bright A_CustomMissile("HunterShotHNTR", 60, 0, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHNTR+2
		MissileHMP:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",5,"MissileEnd")
			HUMA A 5 A_FaceTarget
			HUMA F 4 Bright A_CustomMissile("HunterShot", 60, 0, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHMP+2
		MissileUV:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",6,"MissileEnd")
			HUMA A 5 A_FaceTarget
			HUMA F 4 Bright A_CustomMissile("HunterShotUV", 60, 0, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileUV+2
		MissileNightmare:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",6,"MissileEnd")
			HUMA A 5 A_FaceTarget
			HUMA F 4 Bright A_CustomMissile("HunterShotNightmare", 60, 0, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileNightmare+2
		MissileEnd:
			TNT1 A 0 A_TakeInventory("HunterShotToken",6)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
		Pain:
			HUMA E 6
			TNT1 A 0 A_TakeInventory("HunterShotToken",6)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("HunterShotToken",6)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			HUMA E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			HUMA E 16
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			HUMA E 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			HUMA E 30 A_Pain
			HUMA E 70
			Goto See
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		DukeGibStart:
			HUMA K 1
			TNT1 A 0 A_UnsetSolid
			HUMA K 1 A_SpawnDebris("NewDukeGibs1_MajorHunter")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_MajorHunter")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs3_MajorHunter")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs4_MajorHunter")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs5_MajorHunter")
			TNT1 AAA 0 A_SpawnDebris("NewDukeGibs6_MajorHunter")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMA G 8
			HUMA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Death2")
			HUMA G 2 A_CheckFloor("Death2")
			Goto Death+3
		Death2:
			HUMA H 6 A_Stop
			HUMA I 6 A_NoBlocking
			HUMA J 0 A_Scream
			HUMA J -1 A_KillMaster
			Stop
		Death.Explosive:
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMA G 5
			HUMA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeath2")
			HUMA G 2 A_CheckFloor("XDeath2")
			Goto XDeath+3
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			HUMA K 5 A_PlaySoundEx("hunter/explode","body",0,0)
			HUMA L 4 A_Stop
			HUMA MN 5 A_NoBlocking
			HUMA O -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			HUMA E 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			HUMA E 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			HUMA E 3 A_SetTranslucent(0.9,0)
			HUMA E 0 A_Scream
			HUMA E 3 A_SetTranslucent(0.8,0)
			HUMA E 0 A_NoBlocking
			HUMA E 3 A_SetTranslucent(0.7,0)
			HUMA E 3 A_SetTranslucent(0.6,0)
			HUMA E 3 A_SetTranslucent(0.5,0)
			HUMA E 3 A_SetTranslucent(0.4,0)
			HUMA E 3 A_SetTranslucent(0.3,0)
			HUMA E 3 A_SetTranslucent(0.2,0)
			HUMA E 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			HUMA E 0 A_GenericFreezeDeath
			HUMA E 70 A_UnsetSolid
			HUMA E 0 A_IceGuyDie
			HUMA E 1 A_IceGuyDie
			TNT1 A -1 A_KillMaster
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			HUMA JIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			HUMA A 3 A_SetTranslucent(0.2,0)
			HUMA A 3 A_SetTranslucent(0.3,0)
			HUMA A 3 A_SetTranslucent(0.4,0)
			HUMA A 3 A_SetTranslucent(0.5,0)
			HUMA A 3 A_SetTranslucent(0.6,0)
			HUMA A 3 A_SetTranslucent(0.7,0)
			HUMA A 3 A_SetTranslucent(0.8,0)
			HUMA A 3 A_SetTranslucent(0.9,0)
			HUMA A 3 A_SetTranslucent(1,0)
			HUMA A 1 A_SetShootable
			Goto See
	}
}

ACTOR HunterMajorITYTD : HunterMajor 
{ 
	Speed 4.67 
	Health 150 
}
ACTOR HunterMajorHNTR : HunterMajor 
{ 
	Speed 4.99 
	Health 225 
}
ACTOR HunterMajorUV : HunterMajor 
{ 
	Speed 6 
	Health 375 
}
ACTOR HunterMajorNightmare : HunterMajor 
{ 
	Speed 6.635
	Health 450
}

ACTOR MultiHunterMajor : HunterMajor
{
	Species "HunterMajor"
	+THRUSPECIES
}
ACTOR MultiHunterMajorITYTD : HunterMajorITYTD
{
	Species "HunterMajor"
	+THRUSPECIES
}
ACTOR MultiHunterMajorHNTR : HunterMajorHNTR
{
	Species "HunterMajor"
	+THRUSPECIES
}
ACTOR MultiHunterMajorUV : HunterMajorUV
{
	Species "HunterMajor"
	+THRUSPECIES
}
ACTOR MultiHunterMajorNightmare : HunterMajorNightmare
{
	Species "HunterMajor"
	+THRUSPECIES
}

ACTOR InvisHunterMajor : HunterMajor
{
	States
	{
		Spawn:
			HUMA A 1 A_SetTranslucent(0.0)
			Goto "Super::Spawn"
		See:
			TNT1 A 0 A_SetTranslucent(0.0)
			Goto "Super::See"
		Missile:
			HUMA A 2 A_FaceTarget
			HUMA A 1 A_SetTranslucent(1.0)
			Goto "Super::Missile"
		MissileEnd:
			TNT1 A 0 A_SetTranslucent(0.0)
			Goto "Super::MissileEnd"
		Pain:
			HUMA E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain"
		Pain.MightyBoot:
			HUMA E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain.MightyBoot"
		Pain.MarathonStun:
			HUMA E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain.MarathonStun"
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		Death:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::Death"
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::XDeath"
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::Death.Ice"
	}
}