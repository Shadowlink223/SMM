ACTOR HunterMinor
{
	Health 200
	Radius 24
	Height 64
	Speed 4.425
	Mass 1000
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MinMissileChance 10
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+NOBLOOD
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound ""
	PainSound ""
	DeathSound "hunter/crash"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o's spine became a Minor Pfhor Hunter's trophy."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonPlasmaFire", 0.74
	DamageFactor "MarathonFusion", 1.8
	DamageFactor "Fire", 0.0
	DamageFactor "WolfenFlamethrower", 0.0
	DamageFactor "HexGreenFire", 0.0
	DamageFactor "FireExplosive", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.49
	States 
	{
		Spawn:
			HUMI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			HUMI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			HUMI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			HUMI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			HUMI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17) //17.5
			Goto Spawn2
		Spawn2:
			HUMI A 12 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",6,"See2")
			//TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,2)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMI B 1 A_Chase
			Loop
			HUMI B 1 A_Chase(0,0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",12,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMI C 1 A_Chase
			Loop
			HUMI C 1 A_Chase(0,0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",18,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMI D 1 A_Chase
			Loop
			HUMI D 1 A_Chase(0,0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",24,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			HUMI E 1 A_Chase
			Loop
			HUMI E 1 A_Chase(0,0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			Goto See
		Missile:
			HUMI A 3 A_FaceTarget
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",2,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 Bright A_CustomMissile("HunterShotITYTD", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileITYTD+2
		MissileHNTR:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",140)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",2,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 Bright A_CustomMissile("HunterShotHNTR", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHNTR+2
		MissileHMP:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",2,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 Bright A_CustomMissile("HunterShot", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHMP+2
		MissileUV:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",3,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 Bright A_CustomMissile("HunterShotUV", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileUV+2
		MissileNightmare:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",24)
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",3,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 Bright A_CustomMissile("HunterShotNightmare", 56, 8, Random(-4,4),0, Random(-4,4))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileNightmare+2
		MissileEnd:
			TNT1 A 0 A_TakeInventory("HunterShotToken",3)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
		Pain:
			HUMI E 6
			TNT1 A 0 A_TakeInventory("HunterShotToken",3)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("HunterShotToken",12)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			HUMI E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			HUMI E 16
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			HUMI E 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			HUMI E 30 A_Pain
			HUMI E 70
			Goto See
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		DukeGibStart:
			HUMI K 1
			TNT1 A 0 A_UnsetSolid
			HUMI K 1 A_SpawnDebris("ProtectorDroneGibs1")
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs2")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs3")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs4")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs5")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMI G 8
			HUMI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Death2")
			HUMI G 2 A_CheckFloor("Death2")
			Goto Death+3
		Death2:
			HUMI H 6 A_Stop
			HUMI I 6 A_NoBlocking
			HUMI J 0 A_Scream
			HUMI J -1 A_KillMaster
			Stop
		Death.Explosive:
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMI G 5
			HUMI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeath2")
			HUMI G 2 A_CheckFloor("XDeath2")
			Goto XDeath+3
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			TNT1 A 0 A_SpawnItemEx("HunterExplosion",0,0,32,0,0,0,0,1,0)
			HUMI K 5 Bright A_PlaySoundEx("hunter/explode","body",0,0)
			HUMI L 4 Bright A_Stop
			HUMI MN 5 Bright A_NoBlocking
			HUMI O -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			HUMI E 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			HUMI E 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			HUMI E 3 A_SetTranslucent(0.9,0)
			HUMI E 0 A_Scream
			HUMI E 3 A_SetTranslucent(0.8,0)
			HUMI E 0 A_NoBlocking
			HUMI E 3 A_SetTranslucent(0.7,0)
			HUMI E 3 A_SetTranslucent(0.6,0)
			HUMI E 3 A_SetTranslucent(0.5,0)
			HUMI E 3 A_SetTranslucent(0.4,0)
			HUMI E 3 A_SetTranslucent(0.3,0)
			HUMI E 3 A_SetTranslucent(0.2,0)
			HUMI E 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			HUMI E 0 A_GenericFreezeDeath
			HUMI E 70 A_UnsetSolid
			HUMI E 0 A_IceGuyDie
			HUMI E 1 A_IceGuyDie
			TNT1 A -1 A_KillMaster
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			HUMI JIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			HUMI A 3 A_SetTranslucent(0.2,0)
			HUMI A 3 A_SetTranslucent(0.3,0)
			HUMI A 3 A_SetTranslucent(0.4,0)
			HUMI A 3 A_SetTranslucent(0.5,0)
			HUMI A 3 A_SetTranslucent(0.6,0)
			HUMI A 3 A_SetTranslucent(0.7,0)
			HUMI A 3 A_SetTranslucent(0.8,0)
			HUMI A 3 A_SetTranslucent(0.9,0)
			HUMI A 3 A_SetTranslucent(1,0)
			HUMI A 1 A_SetShootable
			Goto See
	}
}

ACTOR HunterMinorITYTD : HunterMinor 
{ 
	Speed 3.89 
	Health 100 
}
ACTOR HunterMinorHNTR : HunterMinor 
{ 
	Speed 4.155 
	Health 150 
}
ACTOR HunterMinorUV : HunterMinor 
{ 
	Speed 5
	Health 250 
}
ACTOR HunterMinorNightmare : HunterMinor 
{ 
	Speed 5.53 
	Health 300 
}

ACTOR MultiHunterMinor : HunterMinor
{
	Species "HunterMinor"
	+THRUSPECIES
}
ACTOR MultiHunterMinorITYTD : HunterMinorITYTD
{
	Species "HunterMinor"
	+THRUSPECIES
}
ACTOR MultiHunterMinorHNTR : HunterMinorHNTR
{
	Species "HunterMinor"
	+THRUSPECIES
}
ACTOR MultiHunterMinorUV : HunterMinorUV
{
	Species "HunterMinor"
	+THRUSPECIES
}
ACTOR MultiHunterMinorNightmare : HunterMinorNightmare
{
	Species "HunterMinor"
	+THRUSPECIES
}

ACTOR InvisHunterMinor : HunterMinor
{
	States
	{
		Spawn:
			HUMI A 1 A_SetTranslucent(0.0)
			Goto "Super::Spawn"
		See:
			TNT1 A 0 A_SetTranslucent(0.0)
			Goto "Super::See"
		Missile:
			HUMI A 2 A_FaceTarget
			HUMI A 1 A_SetTranslucent(1.0)
			Goto "Super::Missile"
		MissileEnd:
			TNT1 A 0 A_SetTranslucent(0.0)
			Goto "Super::MissileEnd"
		Pain:
			HUMI E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain"
		Pain.MightyBoot:
			HUMI E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain.MightyBoot"
		Pain.MarathonStun:
			HUMI E 1 A_SetTranslucent(0.0)
			Goto "Super::Pain.MarathonStun"
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		Death:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::Death"
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::XDeath"
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_SetTranslucent(1.0)
			Goto "Super::Death.Ice"
	}
}