ACTOR HunterMinor
{
	Health 200
	Radius 12
	Height 56
	Speed 10
	PainChance 255
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	SeeSound ""
	PainSound ""
	DeathSound "hunter/crash"
	ActiveSound ""
	AttackSound ""
	Obituary "%o was shot by a nazi guard."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
	Decal BulletChip
	Scale 0.49
	States 
	{
		Spawn:
			HUMI A 1 A_Look
			Loop
		See: 
			HUMI BB 2 A_Chase
			HUMI B 1 A_Chase
			HUMI CC 2 A_Chase
			HUMI C 1 A_Chase
			HUMI DD 2 A_Chase
			HUMI D 1 A_Chase
			HUMI EE 2 A_Chase
			HUMI E 1 A_Chase
			Loop
		Missile:
			HUMI A 3 A_FaceTarget
			TNT1 A 0 A_JumpIfInventory("HunterShotToken",3,"MissileEnd")
			HUMI A 5 A_FaceTarget
			HUMI F 4 A_CustomMissile("HunterShot", 60, 0, Random(-2,2),0, Random(-2,2))
			TNT1 A 0 A_GiveInventory("HunterShotToken",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto Missile+1
		MissileEnd:
			TNT1 A 0 A_TakeInventory("HunterShotToken",3)
			Goto See
		Pain:
			TNT1 A 0 A_TakeInventory("HunterShotToken",3)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_TakeInventory("HunterShotToken",12)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			HUMI E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			HUMI E 16
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,1)
			Goto XDeath
			TNT1 A 0 A_GiveInventory ("MarathonDukeGib",1)
			Goto XDeath
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"XDeath2")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto XDeath2
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMI G 8
			HUMI G 1 A_CheckFloor("Death2")
			Goto Death+3
		Death2:
			HUMI H 6 A_Stop
			HUMI I 6 A_NoBlocking
			HUMI J 0 A_Scream
			HUMI J -1
			Stop
		Death.MarathonFusion:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			HUMI G 5
			HUMI G 1 A_CheckFloor("XDeath2")
			Goto XDeath+3
		XDeath2:
			TNT1 A 0 A_JumpIfInventory("MarathonDukeGib",1,"DukeGibStart")
			HUMI K 5 A_PlaySoundEx("hunter/explode","body",0,0)
			HUMI L 4 A_Stop
			HUMI MN 5 A_NoBlocking
			HUMI O -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			HUMI E 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			HUMI E 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			HUMI E 3 A_SetTranslucent(0.9,0)
			HUMI E 0 A_Scream
			HUMI E 3 A_SetTranslucent(0.8,0)
			HUMI E 0 A_NoBlocking
			HUMI E 3 A_SetTranslucent(0.7,0)
			HUMI E 3 A_SetTranslucent(0.6,0)
			HUMI E 3 A_SetTranslucent(0.5,0)
			HUMI E 3 A_SetTranslucent(0.4,0)
			HUMI E 3 A_SetTranslucent(0.3,0)
			HUMI E 3 A_SetTranslucent(0.2,0)
			HUMI E 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			HUMI E 0 A_GenericFreezeDeath
			HUMI E 70 A_UnsetSolid
			HUMI E 0 A_IceGuyDie
			HUMI E 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			HUMI JIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			HUMI A 3 A_SetTranslucent(0.2,0)
			HUMI A 3 A_SetTranslucent(0.3,0)
			HUMI A 3 A_SetTranslucent(0.4,0)
			HUMI A 3 A_SetTranslucent(0.5,0)
			HUMI A 3 A_SetTranslucent(0.6,0)
			HUMI A 3 A_SetTranslucent(0.7,0)
			HUMI A 3 A_SetTranslucent(0.8,0)
			HUMI A 3 A_SetTranslucent(0.9,0)
			HUMI A 3 A_SetTranslucent(1,0)
			HUMI A 1 A_SetShootable
			Goto See
	}
}