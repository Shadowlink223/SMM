ACTOR CyborgMajor
{
	Health 450
	Radius 24
	Height 56
	Speed 4.779
	Mass 1000
	PainChance 255
	MaxDropOffHeight 96
	Gravity 0.15
	MinMissileChance 10
	Species "Cyborg"
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	+DONTHURTSPECIES
	SeeSound ""
	PainSound "cyborg/pain"
	DeathSound "cyborg/death"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was tread upon by a Major Cyborg tank."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonFusion", 1.8
	DamageFactor "MarathonPlasmaFire", 0.74
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Bloodcolor Purple
	Decal BulletChip
	Scale 0.49
	States 
	{
		Spawn:
			CYMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			CYMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			CYMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			CYMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			CYMA A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 1 A_GiveInventory("AttackFrequency",315)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 1 A_GiveInventory("AttackFrequency",210)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 1 A_GiveInventory("AttackFrequency",105)
			Goto Spawn2
		SpawnUV:
			TNT1 A 1 A_GiveInventory("AttackFrequency",52)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 1 A_GiveInventory("AttackFrequency",26)
			Goto Spawn2
		Spawn2:
			CYMA A 12 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,"See2")
			//TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,2)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_PlaySound("cyborg/moving",5)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			CYMA A 1 A_Chase
			Loop
			CYMA A 1 A_Chase(0,0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",7,"See3")
			//TNT1 A 0 A_JumpIfInventory("FrameTicCounter",1,2)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			CYMA A 1 A_Chase
			Loop
			CYMA A 1 A_Chase(0,0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",14,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			CYMA B 1 A_Chase
			Loop
			CYMA B 1 A_Chase(0,0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",21,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			CYMA C 1 A_Chase
			Loop
			CYMA C 1 A_Chase(0,0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			Goto See
		Missile:
			CYMA D 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			TNT1 A 0 A_GiveInventory("AttackFrequency",315)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",2,"MissileEnd")
			CYMA DD 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("cyborg/fire")
			CYMA E 10 A_CustomMissile("CyborgGrenadeITYTD", 40, 0, 0,0, 0)
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileITYTD+2
		MissileHNTR:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			TNT1 A 0 A_GiveInventory("AttackFrequency",210)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",2,"MissileEnd")
			CYMA DD 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("cyborg/fire")
			CYMA E 10 A_CustomMissile("CyborgGrenadeHNTR", 40, 0, 0,0, 0)
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHNTR+2
		MissileHMP:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			TNT1 A 0 A_GiveInventory("AttackFrequency",105)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",2,"MissileEnd")
			CYMA DD 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("cyborg/fire")
			CYMA E 10 A_CustomMissile("CyborgGrenade", 40, 0, 0,0, 0)
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileHMP+2
		MissileUV:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			TNT1 A 0 A_GiveInventory("AttackFrequency",52)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_JumpIfInventory("AttackReps",2,"MissileEnd")
			CYMA DD 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("cyborg/fire")
			CYMA E 10 A_CustomMissile("CyborgGrenadeUV", 40, 0, 0,0, 0)
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileUV+2
		MissileNightmare:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",21)
			TNT1 A 0 A_GiveInventory("AttackFrequency",26)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("AttackReps",2,"MissileEnd")
			CYMA DD 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("cyborg/fire")
			CYMA E 10 A_CustomMissile("CyborgGrenadeNightmare", 40, 0, 0,0, 0)
			TNT1 A 0 A_GiveInventory("AttackReps",1)
			TNT1 A 0 A_MonsterRefire(128,"MissileEnd")
			Goto MissileNightmare+2
		MissileEnd:
			TNT1 A 0 A_TakeInventory("AttackReps",2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
		Pain:
			CYMA A 6
			TNT1 A 0 A_TakeInventory("AttackReps",2)
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			CYMA A 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			CYMA A 16
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			CYMA A 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			CYMA A 30 A_Pain
			CYMA A 70
			Goto See
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
			Goto Death
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			CYMA F 1 A_SpawnDebris("NewDukeGibs1_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs3_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs4_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs5_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs6_WolfPurple")
			TNT1 A 0 A_SpawnDebris("DukeShrapnel1")
			TNT1 AAAAAAA 0 A_SpawnDebris("DukeShrapnel1")
			TNT1 AAAA 0 A_SpawnDebris("DukeShrapnel2")
			TNT1 AAAAAAA 0 A_SpawnDebris("DukeShrapnel3")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_TakeInventory("MarathonDukeGib",1)
			TNT1 A 0 A_Jump(96,"Death")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto Death
		Death.DukeDamage:
		Death.DukeSpectral:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Recoil(5)
			CYMA F 6 Bright A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("CyborgExplosion",0,0,32,0,0,0,0,1,0)
			CYMA G 6 Bright A_Scream
			CYMA HIJKL 6 Bright
			CYMA L -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			CYMA A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			CYMA A 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			CYMA A 3 A_SetTranslucent(0.9,0)
			CYMA A 0 A_Pain
			CYMA A 3 A_SetTranslucent(0.8,0)
			CYMA A 0 A_NoBlocking
			CYMA A 3 A_SetTranslucent(0.7,0)
			CYMA A 3 A_SetTranslucent(0.6,0)
			CYMA A 3 A_SetTranslucent(0.5,0)
			CYMA A 3 A_SetTranslucent(0.4,0)
			CYMA A 3 A_SetTranslucent(0.3,0)
			CYMA A 3 A_SetTranslucent(0.2,0)
			CYMA A 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			CYMA A 0 A_GenericFreezeDeath
			CYMA A 70 A_UnsetSolid
			CYMA A 0 A_IceGuyDie
			CYMA A 1 A_IceGuyDie
			TNT1 A -1 A_KillMaster
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			CYMA LKJIHGF 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			CYMA A 3 A_SetTranslucent(0.2,0)
			CYMA A 3 A_SetTranslucent(0.3,0)
			CYMA A 3 A_SetTranslucent(0.4,0)
			CYMA A 3 A_SetTranslucent(0.5,0)
			CYMA A 3 A_SetTranslucent(0.6,0)
			CYMA A 3 A_SetTranslucent(0.7,0)
			CYMA A 3 A_SetTranslucent(0.8,0)
			CYMA A 3 A_SetTranslucent(0.9,0)
			CYMA A 3 A_SetTranslucent(1,0)
			CYMA A 1 A_SetShootable
			Goto See
	}
}

ACTOR CyborgMajorITYTD : CyborgMajor 
{ 
	Speed 4.205 
	Health 225 
}
ACTOR CyborgMajorHNTR : CyborgMajor 
{ 
	Speed 4.492
	Health 337 
}
ACTOR CyborgMajorUV : CyborgMajor 
{ 
	Speed 5.4 
	Health 562 
}
ACTOR CyborgMajorNightmare : CyborgMajor 
{ 
	Speed 5.973
	Health 675
}

ACTOR MultiCyborgMajor : CyborgMajor
{
	Species "CyborgMajor"
	+THRUSPECIES
}
ACTOR MultiCyborgMajorITYTD : CyborgMajorITYTD
{
	Species "CyborgMajor"
	+THRUSPECIES
}
ACTOR MultiCyborgMajorHNTR : CyborgMajorHNTR
{
	Species "CyborgMajor"
	+THRUSPECIES
}
ACTOR MultiCyborgMajorUV : CyborgMajorUV
{
	Species "CyborgMajor"
	+THRUSPECIES
}
ACTOR MultiCyborgMajorNightmare : CyborgMajorNightmare
{
	Species "CyborgMajor"
	+THRUSPECIES
}