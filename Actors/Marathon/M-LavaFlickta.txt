ACTOR LavaFlickta
{
	Health 200
	Radius 18
	Height 56
	Speed 5.83
	PainChance 255
	MaxDropOffHeight 96
	MinMissileChance 10
	Gravity 0.15
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	+DONTRIP
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound ""
	PainSound ""
	DeathSound "flickta/death"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was beaten by rocks for intruding on a Lava F'lickta's territory."
	HitObituary "%o was lacerated for intruding on a Lava F'lickta's territory."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 2.0
	DamageFactor "MarathonPlasmaFire", 0.74
	DamageFactor "Fire", 0.0
	DamageFactor "WolfenFlamethrower", 0.0
	DamageFactor "HexGreenFire", 0.0
	DamageFactor "FireExplosive", 0.0
	DamageFactor "MarathonFusion", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	Scale 0.49
	Bloodcolor Red
	States 
	{
		Spawn:
			LFLI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "SpawnITYTD")
			LFLI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "SpawnHNTR")
			LFLI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "SpawnHMP")
			LFLI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "SpawnUV")
			LFLI A 1 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "SpawnNightmare")
			Goto SpawnITYTD
		SpawnITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",105)
			Goto Spawn2
		SpawnHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			Goto Spawn2
		SpawnHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			Goto Spawn2
		SpawnUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			Goto Spawn2
		SpawnNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",9)
			Goto Spawn2
		Spawn2:
			LFLI A 2 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",5,"See2")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			LFLI B 1 A_Chase("Melee","Missile")
			Loop
			LFLI B 1 A_Chase("Melee",0)
			Loop
		See2:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",10,"See3")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			LFLI C 1 A_Chase("Melee","Missile")
			Loop
			LFLI C 1 A_Chase("Melee",0)
			Loop
		See3:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",15,"See4")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			LFLI D 1 A_Chase("Melee","Missile")
			Loop
			LFLI D 1 A_Chase("Melee",0)
			Loop
		See4:
			TNT1 A 0 A_JumpIfInventory("FrameTicCounter",20,"SeeEnd")
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			TNT1 A 0 A_GiveInventory("FrameTicCounter",1)
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,2)
			LFLI E 1 A_Chase("Melee","Missile")
			Loop
			LFLI E 1 A_Chase("Melee",0)
			Loop
		SeeEnd:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",20)
			Goto See
		Melee:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("AttackFrequency",1,6)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MeleeITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MeleeHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MeleeHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MeleeUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MeleeNightmare")
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_TakeInventory("AttackFrequency",1)
			LFLI A 1 A_FaceTarget
			LFLI A 1 A_JumpIfCloser(75,"Melee")
			Goto See
		MeleeITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",105)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/melee")
			LFLI G 12 A_CustomMeleeAttack((random(20,25)),"flickta/hit",0)
			Goto See
		MeleeHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/melee")
			LFLI G 12 A_CustomMeleeAttack((random(20,25)),"flickta/hit",0)
			Goto See
		MeleeHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/melee")
			LFLI G 12 A_CustomMeleeAttack((random(20,25)),"flickta/hit",0)
			Goto See
		MeleeUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/melee")
			LFLI G 12 A_CustomMeleeAttack((random(20,25)),"flickta/hit",0)
			Goto See
		MeleeNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",9)
			LFLI F 12 A_PlaySound("flickta/melee")
			LFLI G 12 A_CustomMeleeAttack((random(20,25)),"flickta/hit",0)
			Goto See
		Missile:
			TNT1 A 0 A_TakeInventory("FrameTicCounter",32)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 0, "MissileITYTD")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 1, "MissileHNTR")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 2, "MissileHMP")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 3, "MissileUV")
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,3) == 4, "MissileNightmare")
		MissileITYTD:
			TNT1 A 0 A_GiveInventory("AttackFrequency",105)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileITYTD", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			Goto See
		MissileHNTR:
			TNT1 A 0 A_GiveInventory("AttackFrequency",70)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileHNTR", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileITYTD", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			Goto See
		MissileHMP:
			TNT1 A 0 A_GiveInventory("AttackFrequency",35)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectile", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileITYTD", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			Goto See
		MissileUV:
			TNT1 A 0 A_GiveInventory("AttackFrequency",17)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileUV", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileITYTD", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			Goto See
		MissileNightmare:
			TNT1 A 0 A_GiveInventory("AttackFrequency",9)
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileNightmare", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			LFLI F 12 A_PlaySound("flickta/rock")
			LFLI G 12 A_CustomMissile("LavaFlicktaProjectileITYTD", 45, 0, Random(-1.5,1.5),0, Random(-1.5,1.5))
			Goto See
		Pain:
			LFLI M 5
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			LFLI M 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			LFLI M 16
			Goto See
		Death.DukeExplosive:
			Goto XDeathDuke
			/*TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathDuke")
			Goto Death*/
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(5)
			TNT1 A 0 A_Scream
			LFLI H 13
			LFLI HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "Death2")
			LFLI H 2 A_CheckFloor("Death2")
			Goto Death+5
		Death2:
			LFLI I 5 A_Stop
			LFLI J 5 A_NoBlocking
			LFLI K 5
			LFLI L 5 A_PlaySoundEx("pfhor/xdeath","body",0,0)
			LFLI L -1
			Stop
		XDeathDuke:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(5)
			TNT1 A 0 A_Scream
			LFLI H 13
			LFLI HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 2 A_JumpIf(momx < 0.8 && momy < 0.8 && momz == 0, "XDeathDuke2")
			LFLI H 2 A_CheckFloor("XDeathDuke2")
			Goto XDeathDuke+5
		XDeathDuke2:
			SFDG A 1
			TNT1 A 0 A_UnsetSolid
			SFDG A 1 A_SpawnDebris("NewDukeGibs1_FlicktaOrange")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs3")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs3_FlicktaOrange")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs4")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs4_FlicktaOrange")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs5")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs5_FlicktaOrange")
			TNT1 AA 0 A_SpawnDebris("NewDukeGibs6")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs6_FlicktaOrange")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			LFDG A 3 A_NoBlocking
			LFDG B 4 A_Stop
			LFDG CD 4
			LFDG E -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			LFLI M 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			LFLI M 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			LFLI M 3 A_SetTranslucent(0.9,0)
			LFLI M 0 A_Scream
			LFLI M 3 A_SetTranslucent(0.8,0)
			LFLI M 0 A_NoBlocking
			LFLI M 3 A_SetTranslucent(0.7,0)
			LFLI M 3 A_SetTranslucent(0.6,0)
			LFLI M 3 A_SetTranslucent(0.5,0)
			LFLI M 3 A_SetTranslucent(0.4,0)
			LFLI M 3 A_SetTranslucent(0.3,0)
			LFLI M 3 A_SetTranslucent(0.2,0)
			LFLI M 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			LFLI M 0 A_GenericFreezeDeath
			LFLI M 70 A_UnsetSolid
			LFLI M 0 A_IceGuyDie
			LFLI M 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			LFLI KJIH 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			LFLI A 3 A_SetTranslucent(0.2,0)
			LFLI A 3 A_SetTranslucent(0.3,0)
			LFLI A 3 A_SetTranslucent(0.4,0)
			LFLI A 3 A_SetTranslucent(0.5,0)
			LFLI A 3 A_SetTranslucent(0.6,0)
			LFLI A 3 A_SetTranslucent(0.7,0)
			LFLI A 3 A_SetTranslucent(0.8,0)
			LFLI A 3 A_SetTranslucent(0.9,0)
			LFLI A 3 A_SetTranslucent(1,0)
			LFLI A 1 A_SetShootable
			Goto See
	}
}

ACTOR LavaFlicktaITYTD : LavaFlickta
{
	Health 100
	Speed 5.13
}

ACTOR LavaFlicktaHNTR : LavaFlickta
{
	Health 150
	Speed 5.48
}

ACTOR LavaFlicktaUV : LavaFlickta
{
	Health 250
	Speed 6.587
}

ACTOR LavaFlicktaNightmare : LavaFlickta
{
	Health 300
	Speed 7.287
}

ACTOR MultiLavaFlickta : LavaFlickta
{
	Species "LavaFlickta"
	+THRUSPECIES
}
ACTOR MultiLavaFlicktaITYTD : LavaFlicktaITYTD
{
	Species "LavaFlickta"
	+THRUSPECIES
}
ACTOR MultiLavaFlicktaHNTR : LavaFlicktaHNTR
{
	Species "LavaFlickta"
	+THRUSPECIES
}
ACTOR MultiLavaFlicktaUV : LavaFlicktaUV
{
	Species "LavaFlickta"
	+THRUSPECIES
}
ACTOR MultiLavaFlicktaNightmare : LavaFlicktaNightmare
{
	Species "LavaFlickta"
	+THRUSPECIES
}