ACTOR Skitter
{
	Health 100
	Radius 20
	Height 50
	Mass 400
	Speed 10
	PainChance 255
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+LOOKALLAROUND
	SeeSound ""
	PainSound ""
	DeathSound "skitter/death"
	ActiveSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was caught in the sticky strands of a Skitter's web and devoured. Tasty."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Scale 0.4
	Bloodcolor "c8 00 cc"
	States 
	{
		Spawn:
			SKIT A 0
			SKIT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SKIT A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			SKIT A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			SKIT A 2 A_Look
			Loop
		See:
			SKIT ABC 4 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_FaceTarget
			SKIT D 11 A_CustomMissile("SkitterBall",16,0,0,8)
			Goto See
		Pain:
			SKIT E 4
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			SKIT E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			SKIT E 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			SKIT E 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			SKIT E 30 A_Pain
			SKIT E 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
			Goto XDeath
		DukeGibStart:
			SKIT A 1
			SKIT A 0 A_UnsetSolid
			SKIT A 1 A_SpawnDebris("NewDukeGibs1_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs3_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs4_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs5_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs6_WolfPurple")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			Goto Death
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SKIT E 2
			SKIT F 4 A_Scream
			SKIT G 3 A_NoBlocking
			SKIT H -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SKIT E 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			SKIT E 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			SKIT E 3 A_SetTranslucent(0.9,0)
			SKIT E 0 A_Scream
			SKIT E 3 A_SetTranslucent(0.8,0)
			SKIT E 0 A_NoBlocking
			SKIT E 3 A_SetTranslucent(0.7,0)
			SKIT E 3 A_SetTranslucent(0.6,0)
			SKIT E 3 A_SetTranslucent(0.5,0)
			SKIT E 3 A_SetTranslucent(0.4,0)
			SKIT E 3 A_SetTranslucent(0.3,0)
			SKIT E 3 A_SetTranslucent(0.2,0)
			SKIT E 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			SKIT E 0 A_GenericFreezeDeath
			SKIT E 70 A_UnsetSolid
			SKIT E 0 A_IceGuyDie
			SKIT E 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			SKIT HGFE 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			SKIT A 3 A_SetTranslucent(0.2,0)
			SKIT A 3 A_SetTranslucent(0.3,0)
			SKIT A 3 A_SetTranslucent(0.4,0)
			SKIT A 3 A_SetTranslucent(0.5,0)
			SKIT A 3 A_SetTranslucent(0.6,0)
			SKIT A 3 A_SetTranslucent(0.7,0)
			SKIT A 3 A_SetTranslucent(0.8,0)
			SKIT A 3 A_SetTranslucent(0.9,0)
			SKIT A 3 A_SetTranslucent(1,0)
			SKIT A 1 A_SetShootable
			Goto See
	}
}

ACTOR MultiMage : Mage
{
	Species "Mage"
	+THRUSPECIES
}