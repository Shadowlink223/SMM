ACTOR QScrag
{
	Health 10120
	Radius 16
	Height 56
	Speed 15
	Mass 0x7FFFFFFF
	PainChance 0
	Scale 0.5
	WoundHealth 10040
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	+NOGRAVITY
	+FLOAT
	SeeSound "scrag/sight"
	PainSound "scrag/pain"
	DeathSound "scrag/death"
	ActiveSound "scrag/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was scragged by a Scrag."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QSC1 A 0
			QSC1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QSC1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QSC1 A 0 ACS_NamedExecuteAlways("ScragPain",0)
			QSC1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			QSC1 A 4 A_Look
			QSC1 B 3 A_Look
			QSC1 C 4 A_Look
			QSC1 DE 3 A_Look
			QSC1 FG 4 A_Look
			QSC1 H 3 A_Look
			Loop
		See:
			QSC1 P 4 A_Chase
			QSC1 Q 3 A_Chase
			QSC1 R 4 A_Chase
			QSC1 S 3 A_Chase
			QSC1 T 4 A_Chase
			QSC1 U 3 A_Chase
			QSC1 V 4 A_Chase
			QSC1 W 3 A_Chase
			QSC1 X 4 A_Chase
			QSC1 Y 3 A_Chase
			QSC1 Z 4 A_Chase
			QSC2 A 3 A_Chase
			QSC2 B 4 A_Chase
			QSC2 C 3 A_Chase
			Loop
		Missile:
			QSC2 D 4 A_FaceTarget
			QSC2 E 3 A_FaceTarget
			QSC2 F 4 A_FaceTarget
			QSC2 G 3 Bright A_CustomMissile("ScragProjectile",32)
			QSC2 H 4 A_FaceTarget
			QSC2 I 3 A_FaceTarget
			QSC2 H 4 A_FaceTarget
			QSC2 G 3 A_FaceTarget
			QSC2 F 4 Bright A_CustomMissile("ScragProjectile",32)
			QSC2 E 3 A_FaceTarget
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			QSC2 Q 3 A_Pain
			QSC2 R 3
			QSC2 S 4
			QSC2 T 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QSC2 R 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QSC2 R 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QSC2 R 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QSC2 R 30 A_Pain
			QSC2 R 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QSC2 U 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QSC2 U 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QSC2 U 1 A_JumpIfHealthLower(10037,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QSC2 R 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QSC2 R 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QSC2 U 3
			QSC2 V 4
			QSC2 W 3
			QSC2 X 4
			QSC2 Y 3
			QSC2 Z 4
			QSC3 A 3
		Fall:
			QSC3 B 1 A_CheckFloor("Grounded")
			Loop
		Grounded:
			QSC3 B 1 A_PlaySound("quake/land") 
			QSC3 B -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QSC2 R 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QSC2 R 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QSC2 R 3 A_SetTranslucent(0.9,0)
			QSC2 R 0 A_Scream
			QSC2 R 3 A_SetTranslucent(0.8,0)
			QSC2 R 0 A_NoBlocking
			QSC2 R 3 A_SetTranslucent(0.7,0)
			QSC2 R 3 A_SetTranslucent(0.6,0)
			QSC2 R 3 A_SetTranslucent(0.5,0)
			QSC2 R 3 A_SetTranslucent(0.4,0)
			QSC2 R 3 A_SetTranslucent(0.3,0)
			QSC2 R 3 A_SetTranslucent(0.2,0)
			QSC2 R 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QSC2 R 0 A_GenericFreezeDeath
			QSC2 R 70 A_UnsetSolid
			QSC2 R 0 A_IceGuyDie
			QSC2 R 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_SpawnItemEx("ScragHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QSC3 BA 3
			QSC2 ZYXWVU 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QSC1 A 3 A_SetTranslucent(0.2,0)
			QSC1 A 3 A_SetTranslucent(0.3,0)
			QSC1 A 3 A_SetTranslucent(0.4,0)
			QSC1 A 3 A_SetTranslucent(0.5,0)
			QSC1 A 3 A_SetTranslucent(0.6,0)
			QSC1 A 3 A_SetTranslucent(0.7,0)
			QSC1 A 3 A_SetTranslucent(0.8,0)
			QSC1 A 3 A_SetTranslucent(0.9,0)
			QSC1 A 3 A_SetTranslucent(1,0)
			QSC1 A 1 A_SetShootable
			Goto See
	}
}

Actor QScragDG : QScrag
{
	Health 80
	GibHealth 40
	WoundHealth -10000
}

ACTOR ATScrag
{
	Scale 0.5
	States
	{
		Spawn:
			QSC1 ABCDEFGHIJKLMNO 4
			TNT1 A 10
			QSC1 PQRSTUVWXYZ 4
			QSC2 ABC 4
			TNT1 A 10
			QSC2 DEFGDEFG 4
			TNT1 A 10
			QSC2 QRST 4
			TNT1 A 10
			QSC2 UVWXYZ 4
			QSC3 AB 4
			TNT1 A 10
			Loop
	}
}