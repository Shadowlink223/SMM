ACTOR Rottweiler
{
	Health 10060
	Radius 24
	Height 64
	Speed 20
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	WoundHealth 10035
	//MeleeRange 70
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+QUICKTORETALIATE
	SeeSound "rottweiler/sight"
	PainSound "rottweiler/pain"
	DeathSound "rottweiler/death"
	ActiveSound "rottweiler/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was mauled by a Rottweiler"
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "Explosive", 0.5
	DamageFactor "DukeExplosive", 0.5
	DamageFactor "FireExplosive", 0.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QRT1 A 0
			QRT1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QRT1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QRT1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QRT1 A 3 A_Look
			QRT1 B 4 A_Look
			QRT1 CD 3 A_Look
			QRT1 E 4 A_Look
			QRT1 F 3 A_Look
			QRT1 G 4 A_Look
			QRT1 H 3 A_Look
			QRT1 I 4 A_Look
			Loop
		Walk:
			QRT1 J 4 A_Wander
			QRT1 K 3 A_Look
			QRT1 L 4 A_Wander
			QRT1 M 3 A_Look
			QRT1 N 4 A_Wander
			QRT1 O 4 A_Look
			QRT1 P 3 A_Wander
			QRT1 Q 3 A_Look
			Loop
		See:
			QRT1 R 3 A_Chase
			QRT1 ST 4 A_Chase
			QRT1 U 3 A_Chase
			QRT1 V 4 A_Chase
			QRT1 W 3 A_Chase
			QRT1 X 4 A_Chase
			QRT1 YZ 3 A_Chase
			QRT2 AB 4 A_Chase
			QRT2 C 3 A_Chase
			Loop
		/*Leap:
			QRT2 L 4
			QRT2 MN 3
			QRT2 O 4
			QRT2 PQ 3
			QRT2 RS 4
			QRT2 T 3
			*/
		Melee:
			QRT2 D 4
			QRT2 E 3
			QRT2 F 4
			QRT2 G 3 A_CustomMeleeAttack(random(1,24),"rottweiler/attack","rottweiler/attack")
			QRT2 H 4
			QRT2 I 3
			QRT2 J 4
			QRT2 K 3
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Jump(128,"PainB")
			TNT1 A 0 A_Pain
			QRT2 UV 3
			QRT2 W 4
			QRT2 X 3
			QRT2 Y 4
			QRT2 Z 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 A_Pain
			QRT3 A 3
			QRT3 B 4
			QRT3 C 3
			QRT3 D 4
			QRT3 E 3
			QRT3 F 4
			QRT3 GH 3
			QRT3 I 4
			QRT3 J 3
			QRT3 KL 4
			QRT3 M 3
			QRT3 N 4
			QRT3 OP 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QRT2 U 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QRT2 U 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QRT2 U 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QRT2 U 30 A_Pain
			QRT2 U 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QRT3 Q 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QRT3 Q 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QRT3 Q 1 A_JumpIfHealthLower(10032,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QRT2 U 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QRT2 U 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Jump(128,"DeathB")
			QRT3 Q 4
			QRT3 RS 3
			QRT3 T 4
			QRT3 U 3
			QRT3 V 4
			QRT3 W 3
			QRT3 X 4
			TNT1 A 0 A_NoBlocking
			QRT3 Y -1
			Stop
		DeathB:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Jump(128,"DeathB")
			QRT3 Z 3
			QRT4 A 3
			QRT4 BC 4
			QRT4 D 3
			QRT4 E 4
			QRT4 F 3
			QRT4 G 4
			TNT1 A 0 A_NoBlocking
			QRT4 H -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QRT2 U 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QRT2 U 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QRT2 U 3 A_SetTranslucent(0.9,0)
			QRT2 U 0 A_Scream
			QRT2 U 3 A_SetTranslucent(0.8,0)
			QRT2 U 0 A_NoBlocking
			QRT2 U 3 A_SetTranslucent(0.7,0)
			QRT2 U 3 A_SetTranslucent(0.6,0)
			QRT2 U 3 A_SetTranslucent(0.5,0)
			QRT2 U 3 A_SetTranslucent(0.4,0)
			QRT2 U 3 A_SetTranslucent(0.3,0)
			QRT2 U 3 A_SetTranslucent(0.2,0)
			QRT2 U 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QRT2 U 0 A_GenericFreezeDeath
			QRT2 U 70 A_UnsetSolid
			QRT2 U 0 A_IceGuyDie
			QRT2 U 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:	
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_SpawnItemEx("RottweilerHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QRT3 YXWVUTSRQ 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QRT1 A 3 A_SetTranslucent(0.2,0)
			QRT1 A 3 A_SetTranslucent(0.3,0)
			QRT1 A 3 A_SetTranslucent(0.4,0)
			QRT1 A 3 A_SetTranslucent(0.5,0)
			QRT1 A 3 A_SetTranslucent(0.6,0)
			QRT1 A 3 A_SetTranslucent(0.7,0)
			QRT1 A 3 A_SetTranslucent(0.8,0)
			QRT1 A 3 A_SetTranslucent(0.9,0)
			QRT1 A 3 A_SetTranslucent(1,0)
			QRT1 A 1 A_SetShootable
			Goto See
	}
}

Actor RottweilerDG : Rottweiler
{
	Health 25
	GibHealth 35
	WoundHealth -10000
}

ACTOR ATRottweiler
{
	Scale 0.5
	States
	{
		Spawn:
			QRT1 ABCDEFGHI 4
			TNT1 A 10
			QRT1 JKLMNOPQ 4
			TNT1 A 10
			QRT1 RSTUVWXYZ 4
			QRT2 ABC 4
			TNT1 A 10
			QRT2 DEFGHIJK 4
			TNT1 A 10
			QRT2 LMNOPQRST 4
			TNT1 A 10
			QRT2 UVWXYZ 4
			TNT1 A 10
			QRT3 ABCDEFGHIJKLMNOP 4
			TNT1 A 10
			QRT3 QRSTUVWXY 4
			TNT1 A 10
			QRT3 Z 4
			QRT4 ABCDEFGH 4
			TNT1 A 10
			Loop
	}
}