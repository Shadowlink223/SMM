ACTOR QWrath
{
	Health 400
	Radius 20
	Height 56
	Speed 4
	Mass 0x7FFFFFFF
	PainChance 240
	Scale 0.5
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+NOGRAVITY
	+FLOAT
	SeeSound "wrath/sight"
	PainSound "wrath/pain"
	DeathSound "wrath/death"
	Dropitem "DefaultPinataPop"
	Obituary "%o was exploded by a Vore."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
		Spawn:
			QWH1 A 0
			QWH1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QWH1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QWH1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			QWH1 A 4 A_Look
			Loop
		See:
			QWH1 ABCDEFGHIJKL 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump(128,"AttackB","AttackC")
			QWH1 M 2 A_PlaySound("wrath/attack",2)
			QWH1 NOPQRSTUVW 2 A_FaceTarget
			QWH1 X 2 A_CustomMissile("WrathTracer",32)
			QWH1 YZ 2
			Goto See
		AttackB:
			QWH2 A 2 A_PlaySound("wrath/attack",2)
			QWH2 BCDEF 2 A_FaceTarget
			QWH2 G 2 A_CustomMissile("WrathTracer",32)
			QWH2 HIJKLM 2
			Goto See
		AttackC:
			QWH2 N 2 A_PlaySound("wrath/attack",2)
			QWH2 OPQRSTU 2 A_FaceTarget
			QWH2 V 2 A_CustomMissile("WrathTracer",32)
			QWH2 WXYZ 2
			QWH3 AB 2
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Jump(128,"PainB")
			TNT1 A 0 A_Pain
			QWH3 CDEFGH 4
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 A_Pain
			QWH3 IJKLMNOPQRS 4
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			QWH3 TUVWXYZ 3
			QWH4 ABCDEFGH 3
			TNT1 A 0 A_SpawnItemEx("QuakeExplosion")
			TNT1 A 0 A_Explode(60,96)
			TNT1 A 0 A_NoBlocking
			TNT1 A -1
			Stop
	}
}