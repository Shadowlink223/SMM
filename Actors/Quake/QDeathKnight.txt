ACTOR QDeathKnight
{
	Health 10290
	Radius 24
	Height 64
	Speed 18
	Mass 0x7FFFFFFF
	PainChance 0
	Scale 0.5
	WoundHealth 10040
	//MeleeRange 70
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+QUICKTORETALIATE
	SeeSound "qdeathknight/sight"
	PainSound "qdeathknight/pain"
	DeathSound "qdeathknight/death"
	ActiveSound "qdeathknight/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was slain by a Death Knight"
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "Explosive", 0.5
	DamageFactor "DukeExplosive", 0.5
	DamageFactor "FireExplosive", 0.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QDK1 A 0
			QDK1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QDK1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QDK1 A 0 ACS_NamedExecuteAlways("DKnightPain",0)
			QDK1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QDK1 A 3 A_Look
			QDK1 B 4 A_Look
			QDK1 C 3 A_Look
			QDK1 DE 4 A_Look
			QDK1 FG 3 A_Look
			QDK1 H 4 A_Look
			QDK1 I 3 A_Look
			Loop
		Walk:
			QDK1 J 3 A_Look
			QDK1 K 4 A_Wander
			QDK1 L 3 A_Look
			QDK1 M 4 A_Wander
			QDK1 N 4 A_Look
			QDK1 O 3 A_Wander
			QDK1 P 3 A_Look
			QDK1 Q 4 A_Wander
			QDK1 R 3 A_Look
			QDK1 S 4 A_Wander
			QDK1 T 3 A_Look
			QDK1 U 4 A_Wander
			QDK1 V 3 A_Look
			QDK1 W 4 A_Wander
			QDK1 X 3 A_Look
			QDK1 Y 4 A_Wander
			QDK1 Z 3 A_Look
			QDK2 A 4 A_Wander
			QDK2 B 4 A_Look
			QDK2 C 3 A_Wander
			Loop
		See:
			QDK2 D 4 A_Chase
			QDK2 E 3 A_Chase
			QDK2 F 4 A_Chase
			QDK2 G 3 A_Chase
			QDK2 H 4 A_Chase
			QDK2 I 3 A_Chase
			QDK2 J 4 A_Chase
			QDK2 K 3 A_Chase
			TNT1 A 0 A_Jump(192,"See")
			Goto Melee+1
		Melee:
			TNT1 A 0 A_Jump(128,"MeleeB","MeleeC","MeleeD")
			QDK2 LM 3 A_Chase(0,0)
			QDK2 NO 4 A_Chase(0,0)
			QDK2 P 3 A_Chase(0,0)
			QDK2 Q 3 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_Chase(0,0)
			QDK2 R 4 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_Chase(0,0)
			QDK2 S 3 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_Chase(0,0)
			QDK2 T 4 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_Chase(0,0)
			QDK2 U 3 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_Chase(0,0)
			QDK2 V 4 A_CustomMeleeAttack(random(1,9))
			QDK2 W 3 A_Chase(0,0)
			QDK2 X 4 A_Chase(0,0)
			QDK2 Y 3 A_Chase(0,0)
			QDK2 Z 4 A_Chase(0,0)
			QDK3 A 3 A_Chase(0,0)
			Goto See
		MeleeB:
			TNT1 A 0 A_PlaySound("qdeathknight/attack", 5)
			QDK3 B 4
			QDK3 C 3
			QDK3 D 4
			QDK3 E 3
			QDK3 F 4 A_CustomMeleeAttack(random(1,9))
			QDK3 G 3 A_CustomMeleeAttack(random(1,9))
			QDK3 H 4 A_CustomMeleeAttack(random(1,9))
			QDK3 I 3 A_CustomMeleeAttack(random(1,9))
			QDK3 J 4 A_CustomMeleeAttack(random(1,9))
			QDK3 K 3
			Goto See
		MeleeC:
			TNT1 A 0 A_PlaySound("qdeathknight/attack", 5)
			QDK3 L 4 A_FaceTarget
			QDK3 M 3 A_FaceTarget
			QDK3 N 4 A_FaceTarget
			QDK3 O 3 A_FaceTarget
			QDK3 P 4 A_CustomMeleeAttack(random(1,9))
			QDK3 Q 3 A_CustomMeleeAttack(random(1,9))
			QDK3 R 4 A_CustomMeleeAttack(random(1,9))
			QDK3 S 3 A_CustomMeleeAttack(random(1,9))
			QDK3 T 4 A_CustomMeleeAttack(random(1,9))
			QDK3 U 3 A_FaceTarget
			QDK3 V 4 A_FaceTarget
			Goto See
		MeleeD:
			TNT1 A 0 A_PlaySound("qdeathknight/attack", 5)
			QDK3 W 4
			QDK3 X 3
			QDK3 Y 4
			QDK3 Z 3 A_CustomMeleeAttack(random(1,9))
			QDK4 A 4 A_CustomMeleeAttack(random(1,9))
			QDK4 B 3 A_CustomMeleeAttack(random(1,9))
			QDK4 C 4
			QDK4 D 3
			QDK4 E 4
			QDK4 F 3 A_CustomMeleeAttack(random(1,9))
			QDK4 G 4 A_CustomMeleeAttack(random(1,9))
			QDK4 H 3 A_CustomMeleeAttack(random(1,9))
			QDK4 I 4
			QDK4 J 4
			QDK4 K 3
			QDK4 L 3
			QDK4 M 4 A_CustomMeleeAttack(random(1,9))
			QDK4 N 3 A_CustomMeleeAttack(random(1,9))
			QDK4 O 4 A_CustomMeleeAttack(random(1,9))
			QDK4 P 4
			QDK4 Q 3
			QDK4 R 3
			Goto See
		Missile:
			QDK4 S 3 A_FaceTarget
			QDK4 T 4 A_FaceTarget
			QDK4 U 3 A_FaceTarget
			QDK4 V 4 A_FaceTarget
			QDK4 W 3 A_FaceTarget
			QDK4 X 4 A_CustomMissile("DeathKnightSpike",32,10,-10)
			QDK4 Y 3 A_CustomMissile("DeathKnightSpike",32,5,-5)
			QDK4 Z 4 A_CustomMissile("DeathKnightSpike",32,0,0)
			QDK5 A 3 A_CustomMissile("DeathKnightSpike",32,-5,5)
			QDK5 B 3 A_CustomMissile("DeathKnightSpike",32,-10,10)
			QDK5 C 4 A_CustomMissile("DeathKnightSpike",32,-15,15)
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Pain
			QDK5 DE 3
			QDK5 F 4
			QDK5 G 3
			QDK5 H 4
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QDK5 D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QDK5 D 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QDK5 D 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QDK5 D 30 A_Pain
			QDK5 D 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QDK5 I 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QDK5 I 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QDK5 I 1 A_JumpIfHealthLower(10037,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QDK5 D 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QDK5 D 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Jump(128,"DeathB")
			QDK5 I 4
			QDK5 J 3
			QDK5 K 4
			QDK5 L 3
			QDK5 M 4
			QDK5 N 3
			QDK5 O 4
			QDK5 P 3
			QDK5 Q 4
			QDK5 R 3
			QDK5 S 4
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_KillMaster
			QDK5 T -1
			Stop
		DeathB:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Jump(128,"DeathB")
			QDK5 U 3
			QDK5 V 4
			QDK5 W 3
			QDK5 X 4
			QDK5 Y 3
			QDK5 Z 4
			QDK6 A 4
			QDK6 B 3
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_KillMaster
			QDK6 C -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QDK5 D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QDK5 D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QDK5 D 3 A_SetTranslucent(0.9,0)
			QDK5 D 0 A_Scream
			QDK5 D 3 A_SetTranslucent(0.8,0)
			QDK5 D 0 A_NoBlocking
			QDK5 D 3 A_SetTranslucent(0.7,0)
			QDK5 D 3 A_SetTranslucent(0.6,0)
			QDK5 D 3 A_SetTranslucent(0.5,0)
			QDK5 D 3 A_SetTranslucent(0.4,0)
			QDK5 D 3 A_SetTranslucent(0.3,0)
			QDK5 D 3 A_SetTranslucent(0.2,0)
			QDK5 D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A 0 A_KillMaster
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_KillMaster
			QDK5 D 0 A_GenericFreezeDeath
			QDK5 D 70 A_UnsetSolid
			QDK5 D 0 A_IceGuyDie
			QDK5 D 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_SpawnItemEx("DeathKnightHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			TNT1 A 0 A_KillMaster
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QDK5 TSRQPONMLKJI 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QDK1 A 3 A_SetTranslucent(0.2,0)
			QDK1 A 3 A_SetTranslucent(0.3,0)
			QDK1 A 3 A_SetTranslucent(0.4,0)
			QDK1 A 3 A_SetTranslucent(0.5,0)
			QDK1 A 3 A_SetTranslucent(0.6,0)
			QDK1 A 3 A_SetTranslucent(0.7,0)
			QDK1 A 3 A_SetTranslucent(0.8,0)
			QDK1 A 3 A_SetTranslucent(0.9,0)
			QDK1 A 3 A_SetTranslucent(1,0)
			QDK1 A 1 A_SetShootable
			Goto See
	}
}

Actor QDeathKnightDG : QDeathKnight
{
	Health 250
	GibHealth 40
	WoundHealth -10000
}

ACTOR ATDeathKnight
{
	MONSTER
	Radius 24
	Height 64
	Health 0x7FFFFFFF
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	States
	{
		Spawn:
		See:
			QDK1 ABCDEFGHI 4
			Loop
		Pain:
			TNT1 A 1 A_GiveInventory("AttackFrequency",1)
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",11,"State11")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",10,"State10")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",9,"State9")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",8,"State8")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",7,"State7")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",6,"State6")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",5,"State5")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",4,"State4")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",3,"State3")
			TNT1 A 1 A_JumpIfInventory("AttackFrequency",2,"State2")
			TNT1 A 1 A_TakeInventory("AttackFrequency",15)
			TNT1 A 1
			Loop
		State2:
			QDK1 JKLMNOPQRSTUVWXYZ 4
			QDK2 ABC 4
			Loop
		State3:
			QDK2 DEFGHIJK 4
			Loop
		State4:
			QDK2 LMNOPQRSTUVWXYZ 4
			QDK3 A 4
			Loop
		State5:
			QDK3 BCDEFGHIJK 4
			Loop
		State6:
			QDK3 LMNOPQRSTUV 4
			Loop
		State7:
			QDK3 WXYZ 4
			QDK4 ABCDEFGHIJKLMNOPQR 4
			Loop
		State8:
			QDK4 STUVWXYZ 4
			QDK5 ABC 4
			Loop
		State9:
			QDK5 DEFGH 4
			Loop
		State10:
			QDK5 IJKLMNOPQRST 4
			Loop
		State11:
			QDK5 UVWXYZ 4
			QDK6 ABC 4
			Loop
	}
}