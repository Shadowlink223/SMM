ACTOR QGrunt
{
	Health 10065
	Radius 16
	Height 56
	Speed 10
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	WoundHealth 10035
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	DropItem "QuakeShellBackpack"
	DropItem "DefaultPinataPop"
	SeeSound "qgrunt/sight"
	PainSound "qgrunt/pain2"
	DeathSound "qgrunt/death"
	ActiveSound "qgrunt/active"
	AttackSound "qgrunt/fire"
	Obituary "%o was shot by a Grunt."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QGR1 A 0
			QGR1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QGR1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QGR1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Prowl")
		Stand:
			QGR1 A 4 A_Look
			QGR1 B 3 A_Look
			QGR1 C 4 A_Look
			QGR1 D 3 A_Look
			QGR1 E 4 A_Look
			QGR1 F 3 A_Look
			QGR1 G 4 A_Look
			QGR1 H 3 A_Look
			Loop
		Prowl:
			TNT1 A 0
			QGR1 I 4 A_Wander
			QGR1 J 3 A_Look
			QGR1 K 4 A_Wander
			QGR1 L 4 A_Look
			QGR1 M 3 A_Wander
			QGR1 N 4 A_Look
			QGR1 O 3 A_Wander
			QGR1 P 4 A_Look
			QGR1 Q 3 A_Wander
			QGR1 R 3 A_Look
			QGR1 S 4 A_Wander
			QGR1 T 3 A_Look
			QGR1 U 4 A_Wander
			QGR1 V 4 A_Look
			QGR1 W 3 A_Wander
			QGR1 X 4 A_Look
			QGR1 Y 3 A_Wander
			QGR1 Z 4 A_Look
			QGR2 A 4 A_Wander
			QGR2 B 2 A_Look
			QGR2 C 4 A_Wander
			QGR2 D 3 A_Look
			QGR2 E 4 A_Wander
			QGR2 F 4 A_Look
			Loop
		See:
			QGR2 G 3 A_Chase
			QGR2 H 4 A_Chase
			QGR2 I 3 A_Chase
			QGR2 J 4 A_Chase
			QGR2 K 3 A_Chase
			QGR2 L 4 A_Chase
			QGR2 M 3 A_Chase
			QGR2 N 4 A_Chase
			Loop
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Jump(102,"PainB","PainC")
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySound("qgrunt/pain1", CHAN_VOICE)
			QGR3 IJ 3
			QGR3 KL 4
			QGR3 MN 3
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,39)
			TNT1 A 0 A_Pain
			QGR3 OPQ 3
			QGR3 R 4
			QGR3 S 3
			QGR3 T 4
			QGR3 U 3
			QGR3 VW 4
			QGR3 XY 3
			QGR3 Z 4
			QGR4 AB 3
			QGR4 B 4
			Goto See
		PainC:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,39)
			TNT1 A 0 A_Pain
			QGR4 CD 3
			QGR4 E 4
			QGR4 FG 3
			QGR4 H 4
			QGR4 I 3
			QGR4 JK 4
			QGR4 L 3
			QGR4 M 4
			QGR4 NO 3
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QGR3 J 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QGR3 J 16 A_PlaySound("qgrunt/pain1", CHAN_VOICE)
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QGR3 J 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QGR3 J 30 A_PlaySound("qgrunt/pain1", CHAN_VOICE)
			QGR3 J 70
			Goto See
		Missile:
			QGR2 O 4 A_FaceTarget
			QGR2 P 3 A_FaceTarget
			QGR2 Q 4 A_FaceTarget
			QGR2 R 3 //A_FaceTarget
			QGR2 S 4 Bright A_CustomBulletAttack(12,12,4,4,"QuakeBulletPuff",0,CBAF_NORANDOM|CBAF_AIMFACING)
			QGR2 T 3
			QGR2 U 4
			QGR2 V 3
			QGR2 W 4
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QGR4 P 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QGR4 P 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QGR4 P 1 A_JumpIfHealthLower(10032,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QGR3 L 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QGR3 L 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Jump(128,"DeathC")
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QGR4 P 3
			QGR4 Q 4
			QGR4 RS 3
			QGR4 T 4
			QGR4 U 3
			QGR4 VW 4
			QGR4 X 3
			QGR4 Y -1
			Stop
		DeathC:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QGR4 Z 3
			QGR5 A 4
			QGR5 BC 3
			QGR5 D 4
			QGR5 E 3
			QGR5 FG 4
			QGR5 HI 3
			QGR5 J -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QGR3 L 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QGR3 L 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QGR3 L 3 A_SetTranslucent(0.9,0)
			QGR3 L 0 A_Scream
			QGR3 L 3 A_SetTranslucent(0.8,0)
			QGR3 L 0 A_NoBlocking
			QGR3 L 3 A_SetTranslucent(0.7,0)
			QGR3 L 3 A_SetTranslucent(0.6,0)
			QGR3 L 3 A_SetTranslucent(0.5,0)
			QGR3 L 3 A_SetTranslucent(0.4,0)
			QGR3 L 3 A_SetTranslucent(0.3,0)
			QGR3 L 3 A_SetTranslucent(0.2,0)
			QGR3 L 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QGR3 L 0 A_GenericFreezeDeath
			QGR3 L 70 A_UnsetSolid
			QGR3 L 0 A_IceGuyDie
			QGR3 L 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("GruntHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QGR4 YXWVUTSRQP 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QGR1 A 3 A_SetTranslucent(0.2,0)
			QGR1 A 3 A_SetTranslucent(0.3,0)
			QGR1 A 3 A_SetTranslucent(0.4,0)
			QGR1 A 3 A_SetTranslucent(0.5,0)
			QGR1 A 3 A_SetTranslucent(0.6,0)
			QGR1 A 3 A_SetTranslucent(0.7,0)
			QGR1 A 3 A_SetTranslucent(0.8,0)
			QGR1 A 3 A_SetTranslucent(0.9,0)
			QGR1 A 3 A_SetTranslucent(1,0)
			QGR1 A 1 A_SetShootable
			Goto See
	}
}

Actor QGruntDG : QGrunt
{
	Health 30
	GibHealth 35
	WoundHealth -10000
}

ACTOR ATGrunt
{
	Scale 0.5
	States
	{
		Spawn:
			QGR1 ABCDEFGH 4
			TNT1 A 10
			QGR1 IJKLMNOPQRSTUVWXYZ 4
			QGR2 ABCDEF 4
			TNT1 A 10
			QGR2 GHIJKLMN 4
			TNT1 A 10
			QGR2 OPQRSTUVW 4
			TNT1 A 10
			QGR3 IJKLMN 4
			TNT1 A 10
			QGR3 OPQRSTUVWXYZ 4
			QGR4 AB 4
			TNT1 A 10
			QGR4 CDEFGHIJKLMNO 4
			TNT1 A 10
			QGR4 PQRSTUVWXY 4
			TNT1 A 10
			QGR4 Z 4
			QGR5 ABCDEFGHIJ 4
			TNT1 A 10
			Loop
	}
}