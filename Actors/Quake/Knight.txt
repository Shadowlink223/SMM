ACTOR QKnight
{
	Health 10115
	Radius 24
	Height 64
	Speed 12
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	WoundHealth 10040
	MeleeRange 50
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	SeeSound "qknight/sight"
	PainSound "qknight/pain"
	DeathSound "qknight/death"
	ActiveSound "qknight/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was slashed by a Knight."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "Explosive", 0.5
	DamageFactor "DukeExplosive", 0.5
	DamageFactor "FireExplosive", 0.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QKN1 A 0
			QKN1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QKN1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QKN1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QKN1 A 3 A_Look
			QKN1 B 4 A_Look
			QKN1 C 3 A_Look
			QKN1 D 4 A_Look
			QKN1 E 3 A_Look
			QKN1 F 4 A_Look
			QKN1 G 3 A_Look
			QKN1 H 4 A_Look
			QKN1 I 3 A_Look
			Loop
		Walk:
			QKN1 JKLM 5 A_Wander
			QKN1 NOPQ 5 A_Look
			QKN1 RSTU 5 A_Wander
			QKN1 VW 5 A_Look
			Loop
		See:
			QKN1 X 3 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN1 Y 4 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN1 Z 3 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN2 A 4 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN2 B 3 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN2 C 4 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN2 D 3 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			QKN2 E 4 A_Chase
			TNT1 A 0 A_JumpIfCloser(80,"Melee")
			Loop
		Melee:
			TNT1 A 0 A_JumpIfCloser(50,"MeleeB")
			QKN2 F 4 A_PlaySound("qknight/attack1", 5)
			TNT1 A 0 A_FaceTarget
			QKN2 G 3 A_Recoil(-2)
			TNT1 A 0 A_FaceTarget
			QKN2 H 4 A_Recoil(-2)
			TNT1 A 0 A_FaceTarget
			QKN2 I 3 A_Recoil(-2)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 J 4 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 K 3 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 L 4 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 M 3 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 N 4 A_CustomMeleeAttack(random(1,9))
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 O 3 A_FaceTarget
			TNT1 A 0 A_Recoil(-2)
			QKN2 P 4 A_FaceTarget
			TNT1 A 0
			Goto See
		MeleeB:
			QKN2 Q 4 A_PlaySound("qknight/attack1", 5)
			QKN2 R 3 A_FaceTarget
			QKN2 S 4 A_FaceTarget
			QKN2 T 3 A_FaceTarget
			QKN2 U 4 A_FaceTarget
			QKN2 V 3 A_CustomMeleeAttack(random(1,9))
			QKN2 W 4 A_CustomMeleeAttack(random(1,9))
			QKN2 X 3 A_CustomMeleeAttack(random(1,9))
			QKN2 Y 4 A_FaceTarget
			QKN2 Z 3 A_FaceTarget
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			TNT1 A 0 A_Jump(38,"PainB")
			TNT1 A 0 A_Pain
			QKN3 A 4
			QKN3 BC 3
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 A_Pain
			QKN3 D 4
			QKN3 E 3
			QKN3 F 4
			QKN3 GH 3
			QKN3 IJ 4
			QKN3 K 3
			QKN3 L 4
			QKN3 M 3
			QKN3 N 4
			TNT1 A 0
			Goto See
		Wound:
			QRT3 O 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QGR3 L 1 A_Die("Zorch")
			Goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Jump(128,"DeathB")
			QKN3 O 4
			QKN3 PQ 3
			QKN3 R 4
			QKN3 S 3
			QKN3 TU 4
			QKN3 VW 3
			TNT1 A 0 A_NoBlocking
			QKN3 X -1
			Stop
		DeathB:
			TNT1 A 0 A_Scream
			QKN3 YZ 3
			QKN4 A 4
			QKN4 B 3
			QKN4 C 4
			QKN4 D 3
			QKN4 E 4
			QKN4 F 3
			QKN4 G 4
			QKN4 H 3
			TNT1 A 0 A_NoBlocking
			QKN4 I -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QKN3 B 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QKN3 B 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QKN3 B 3 A_SetTranslucent(0.9,0)
			QKN3 B 0 A_Scream
			QKN3 B 3 A_SetTranslucent(0.8,0)
			QKN3 B 0 A_NoBlocking
			QKN3 B 3 A_SetTranslucent(0.7,0)
			QKN3 B 3 A_SetTranslucent(0.6,0)
			QKN3 B 3 A_SetTranslucent(0.5,0)
			QKN3 B 3 A_SetTranslucent(0.4,0)
			QKN3 B 3 A_SetTranslucent(0.3,0)
			QKN3 B 3 A_SetTranslucent(0.2,0)
			QKN3 B 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_PlaySound("quake/gib", CHAN_VOICE)
			TNT1 A 0 A_SpawnItemEx("KnightHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 AA 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QKN4 IHGFEDCBA 3
			QKN3 ZY 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QKN1 A 3 A_SetTranslucent(0.2,0)
			QKN1 A 3 A_SetTranslucent(0.3,0)
			QKN1 A 3 A_SetTranslucent(0.4,0)
			QKN1 A 3 A_SetTranslucent(0.5,0)
			QKN1 A 3 A_SetTranslucent(0.6,0)
			QKN1 A 3 A_SetTranslucent(0.7,0)
			QKN1 A 3 A_SetTranslucent(0.8,0)
			QKN1 A 3 A_SetTranslucent(0.9,0)
			QKN1 A 3 A_SetTranslucent(1,0)
			QKN1 A 1 A_SetShootable
			Goto See
	}
}

Actor QKnightDG : QKnight
{
	Health 75
	GibHealth 40
	WoundHealth -10000
}

ACTOR ATKnight
{
	Scale 0.5
	States
	{
		Spawn:
			QKN1 ABCDEFGHI 4
			TNT1 A 10
			QKN1 JKLMNOPQRSTUVW 4
			TNT1 A 10
			QKN1 XYZ 4
			QKN2 ABCDE 4
			TNT1 A 10
			QKN2 GHIJKLMNOP 4
			TNT1 A 10
			QKN2 QRSTUVWXYZ 4
			TNT1 A 10
			QKN3 ABC 4
			TNT1 A 10
			QKN3 DEFGHIJKLMN 4
			TNT1 A 10
			QKN3 OPQRSTUVWX 4
			TNT1 A 10
			QKN3 YZ 4
			QKN4 ABCDEFGHI 4
			TNT1 A 10
			Loop
	}
}