ACTOR Fiend
{
	Health 10380
	Radius 24
	Height 64
	Speed 12
	Mass 0x7FFFFFFF
	PainChance 40
	Scale 0.5
	WoundHealth 10080
	//MeleeRange 70
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	SeeSound "fiend/sight"
	PainSound "fiend/pain"
	DeathSound "fiend/death"
	ActiveSound "fiend/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was smashed by a Shambler."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "Explosive", 0.5
	DamageFactor "DukeExplosive", 0.5
	DamageFactor "FireExplosive", 0.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
		Spawn:
			QFT1 A 0
			QFT1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QFT1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QFT1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QFI1 ABCDEFGHIJKLM 3 A_Look
			Loop
		Walk:
			QFI1 NOPQ 5 A_Wander
			QFI1 RSTU 5 A_Look
			Loop
		See:
			QFI1 VWXYZ 3 A_Chase
			QFI2 A 3 A_Chase
			Loop
		Melee:
			QFI2 BCDEFG 3
			QFI2 H 3 A_CustomMeleeAttack(random(10,15),"fiend/attack")
			QFI2 IJKL 3
			QFI2 M 3 A_CustomMeleeAttack(random(10,15),"fiend/attack")
			QFI2 NOP 3
			Goto See
		Missile:
			TNT1 A 0 A_PlaySound("fiend/leap")
			QFI2 QRST 3 A_FaceTarget
			QFI2 U 0 ThrustThingZ(0,70,0,0)
			TNT1 A 0 ThrustThing(angle*256/360,30,0,0)
			QFI2 UVWXYZ 3
			QFI3 AB 3
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Pain
			QFI3 CDEFGH 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Wound:
			QFI3 I 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			QFI3 IJKLMNOPQ 3
			TNT1 A 0 A_NoBlocking
			QFI3 Q -1
			Stop
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_PlaySound("quake/gib", CHAN_VOICE)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Blood", 0, 0, random(8, 56), random(-4, 4), 0, random(0, 6))
			TNT1 AA 0 A_SpawnItemEx("QuakeSmallGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 0 A_KillMaster
			Stop
	}
}