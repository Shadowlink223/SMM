ACTOR Shambler
{
	Health 10660
	Radius 24
	Height 64
	Speed 16
	Mass 0x7FFFFFFF
	PainChance 0
	Scale 0.5
	WoundHealth 10060
	MeleeRange 70
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+QUICKTORETALIATE
	SeeSound "shambler/sight"
	PainSound "shambler/pain"
	DeathSound "shambler/death"
	ActiveSound "shambler/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was smashed by a Shambler."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "Explosive", 0.5
	DamageFactor "DukeExplosive", 0.5
	DamageFactor "FireExplosive", 0.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QSH1 A 0
			QSH1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QSH1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QSH1 A 0 ACS_NamedExecuteAlways("ShamblerPain",0)
			QSH1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QSH1 AB 4 A_Look
			QSH1 CD 3 A_Look
			QSH1 E 4 A_Look
			QSH1 F 3 A_Look
			QSH1 G 4 A_Look
			QSH1 H 3 A_Look
			QSH1 IJ 4 A_Look
			QSH1 KL 3 A_Look
			QSH1 M 4 A_Look
			QSH1 N 3 A_Look
			QSH1 OP 4 A_Look
			QSH1 Q 3 A_Look
			Loop
		Walk:
			QSH1 R 3 A_Look
			QSH1 S 4 A_Wander
			QSH1 T 3 A_Look
			QSH1 U 4 A_Wander
			QSH1 V 3 A_Look
			QSH1 W 4 A_Wander
			QSH1 X 3 A_Look
			QSH1 Y 4 A_Wander
			QSH1 Z 3 A_Look
			QSH2 A 4 A_Look
			QSH2 B 3 A_Wander
			QSH2 C 4 A_Look
			TNT1 A 0 A_Wander
			Loop
		See:
			QSH2 D 3 A_Chase
			QSH2 EF 4 A_Chase
			QSH2 G 3 A_Chase
			QSH2 HI 4 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_Jump(256,"SwingL","SwingR","Smash")
		SwingL:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("shambler/melee2",CHAN_VOICE)
			QSH2 J 4 A_FaceTarget
			QSH2 K 3 A_FaceTarget
			QSH2 L 4 A_FaceTarget
			QSH2 M 3 A_FaceTarget
			QSH2 N 4 A_FaceTarget
			QSH2 O 3 A_FaceTarget
			QSH2 P 3 A_CustomMeleeAttack(60,"shambler/smack")
			QSH2 QR 4
			Goto See
		SwingR:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("shambler/melee",CHAN_VOICE)
			QSH2 ST 3 A_FaceTarget
			QSH2 UV 4 A_FaceTarget
			QSH2 W 3 A_FaceTarget
			QSH2 X 4 A_FaceTarget
			QSH2 Y 3 A_CustomMeleeAttack(60,"shambler/smack")
			QSH2 Z 3
			QSH3 A 4
			Goto See
		Smash:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("shambler/melee",CHAN_VOICE)
			QSH3 B 3 A_FaceTarget
			QSH3 C 4 A_FaceTarget
			QSH3 D 3 A_FaceTarget
			QSH3 E 4 A_FaceTarget
			QSH3 F 3 A_FaceTarget
			QSH3 G 4 A_FaceTarget
			QSH3 H 3 A_FaceTarget
			QSH3 I 4 A_FaceTarget
			QSH3 J 3 A_FaceTarget
			QSH3 K 4 A_CustomMeleeAttack(120,"shambler/smack")
			QSH3 LM 3
			Goto See
		Missile:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("shambler/charge",CHAN_WEAPON)
			QSH3 N 4 A_FaceTarget
			QSH3 O 3 A_FaceTarget
			QSH3 P 5 Bright A_FaceTarget
			QSH3 P 5 A_FaceTarget
			QSH3 QQ 2 Bright A_FaceTarget 
			QSH3 R 4 A_FaceTarget
			TNT1 A 0 A_PlaySound("shambler/boom",CHAN_WEAPON)
			TNT1 A 0 A_CustomMissile("ShamblerBolt",80)
			QSH3 S 3 A_CustomBulletAttack(0,0,1,10,0,512,CBAF_NORANDOM)
			TNT1 A 0 A_CustomMissile("ShamblerBolt",80)
			QSH3 V 4 A_CustomBulletAttack(0,0,1,10,0,512,CBAF_NORANDOM)
			TNT1 A 0 A_CustomMissile("ShamblerBolt",80)
			QSH3 W 3 A_CustomBulletAttack(0,0,1,10,0,512,CBAF_NORANDOM)
			QSH3 X 4
			QSH3 Y 3
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Pain
			QSH3 Z 3
			QSH4 ABCDE 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QSH4 A 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QSH4 A 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QSH4 A 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QSH4 A 30 A_Pain
			QSH4 A 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QSH4 F 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QSH4 F 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QSH4 F 1 A_JumpIfHealthLower(10057,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QSH4 A 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QSH4 A 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			QSH4 F 3
			QSH4 G 4
			QSH4 HI 3
			QSH4 J 4
			QSH4 K 3
			QSH4 LM 4
			QSH4 NOP 3
			TNT1 A 0 A_NoBlocking
			QSH4 P -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QSH4 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QSH4 A 3 A_SpawnItem("ZorchEffectBig")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QSH4 A 3 A_SetTranslucent(0.9,0)
			QSH4 A 0 A_Scream
			QSH4 A 3 A_SetTranslucent(0.8,0)
			QSH4 A 0 A_NoBlocking
			QSH4 A 3 A_SetTranslucent(0.7,0)
			QSH4 A 3 A_SetTranslucent(0.6,0)
			QSH4 A 3 A_SetTranslucent(0.5,0)
			QSH4 A 3 A_SetTranslucent(0.4,0)
			QSH4 A 3 A_SetTranslucent(0.3,0)
			QSH4 A 3 A_SetTranslucent(0.2,0)
			QSH4 A 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QSH4 A 0 A_GenericFreezeDeath
			QSH4 A 70 A_UnsetSolid
			QSH4 A 0 A_IceGuyDie
			QSH4 A 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_SpawnItemEx("ShamblerHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			TNT1 A 0 A_KillMaster
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QSH4 PONMLKJIHGF 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QSH1 A 3 A_SetTranslucent(0.2,0)
			QSH1 A 3 A_SetTranslucent(0.3,0)
			QSH1 A 3 A_SetTranslucent(0.4,0)
			QSH1 A 3 A_SetTranslucent(0.5,0)
			QSH1 A 3 A_SetTranslucent(0.6,0)
			QSH1 A 3 A_SetTranslucent(0.7,0)
			QSH1 A 3 A_SetTranslucent(0.8,0)
			QSH1 A 3 A_SetTranslucent(0.9,0)
			QSH1 A 3 A_SetTranslucent(1,0)
			QSH1 A 1 A_SetShootable
			Goto See
	}
}

Actor ShamblerDG : Shambler
{
	Health 600
	GibHealth 60
	WoundHealth -10000
}

ACTOR ATShambler
{
	Scale 0.5
	States
	{
		Spawn:
			QSH1 ABCDEFGHIJKLMNOPQ 4
			TNT1 A 10
			QSH1 RSTUVWXYZ 4
			QSH2 ABC 4
			TNT1 A 10
			QSH2 DEFGHI 4
			TNT1 A 10
			QSH2 JKLMNOPQR 4
			TNT1 A 10
			QSH2 STUVWXYZ 4
			QSH3 A 4
			TNT1 A 10
			QSH3 BCDEFGHIJKLM 4
			TNT1 A 10
			QSH3 NOPQRSTUVWXY 4
			TNT1 A 10
			QSH3 Z 4
			QSH4 ABCDE 4
			TNT1 A 10
			QSH4 FGHIJKLMNOP 4
			TNT1 A 10
			Loop
	}
}