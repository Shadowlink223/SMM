ACTOR QEnforcer
{
	Health 10115
	Radius 16
	Height 56
	Speed 15
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	WoundHealth 10035
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	SeeSound "qenforcer/sight"
	PainSound "qenforcer/pain"
	DeathSound "qenforcer/death"
	ActiveSound "qenforcer/active"
	Dropitem "DefaultPinataPop"
	DropItem "QuakeCellBackpack"
	Obituary "%o was blasted by a Enforcer"
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QEN1 A 0
			QEN1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QEN1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QEN1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QEN1 A 4 A_Look
			QEN1 B 3 A_Look
			QEN1 C 4 A_Look
			QEN1 D 3 A_Look
			QEN1 E 4 A_Look
			QEN1 F 3 A_Look
			QEN1 G 4 A_Look
			Loop
		Walk:
			TNT1 A 0
			QEN1 H 3 A_Wander
			QEN1 I 3 A_Look
			QEN1 J 4 A_Wander
			QEN1 K 3 A_Look
			QEN1 L 4 A_Wander
			QEN1 M 4 A_Look
			QEN1 N 3 A_Wander
			QEN1 O 4 A_Look
			QEN1 P 3 A_Wander
			QEN1 Q 3 A_Look
			QEN1 R 4 A_Wander
			QEN1 S 3 A_Look
			QEN1 T 4 A_Wander
			QEN1 U 4 A_Look
			QEN1 V 3 A_Wander
			QEN1 W 4 A_Look
			Loop
		See:
			QEN1 XY 3 A_Chase
			QEN1 Z 4 A_Chase
			QEN2 A 4 A_Chase
			QEN2 B 3 A_Chase
			QEN2 C 4 A_Chase
			QEN2 D 3 A_Chase
			QEN2 E 4 A_Chase
			Loop
		Missile:
			QEN2 FG 3 A_FaceTarget
			QEN2 HI 4 A_FaceTarget
			QEN2 J 3 A_FaceTarget
			QEN2 K 4 Bright A_CustomMissile("QEnforcerLaser",45,12)
			QEN2 L 3 A_FaceTarget
			QEN2 M 4 A_FaceTarget
			QEN2 J 3
			QEN2 K 3 Bright A_CustomMissile("QEnforcerLaser",45,12)
			QEN2 L 5
			QEN2 M 2
			QEN2 NO 4
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Jump(51,"PainB")
			TNT1 A 0 A_Jump(77,"PainC","PainD")
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			QEN2 P 4 A_Pain
			QEN2 QR 3
			QEN2 S 4
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			QEN2 T 3 A_Pain
			QEN2 U 4
			QEN2 V 3
			QEN2 W 4
			QEN3 X 3
			TNT1 A 0
			Goto See
		PainC:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			QEN2 Y 4 A_Pain
			QEN2 Z 3
			QEN3 A 4
			QEN3 B 3
			QEN3 C 4
			QEN3 D 3
			QEN3 E 4
			QEN3 F 3
			TNT1 A 0
			Goto See
		PainD:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,70)
			QEN3 G 3 A_Pain
			QEN3 H 4
			QEN3 I 3
			QEN3 J 4
			QEN3 KL 3
			QEN3 MN 4
			QEN3 O 3
			QEN3 P 4
			QEN3 QR 3
			QEN3 S 4
			QEN3 T 3
			QEN3 UV 4
			QEN3 WX 3
			QEN3 Y 4
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QEN2 S 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QEN2 S 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QEN2 S 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QEN2 S 30 A_Pain
			QEN2 S 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QEN3 Z 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QEN3 Z 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QEN3 Z 1 A_JumpIfHealthLower(10032,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QEN2 S 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QEN2 Z 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Jump(128,"FDeath")
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QEN3 Z 3
			QEN4 A 3
			QEN4 B 4
			QEN4 C 3 
			QEN4 D 4 
			QEN4 E 3
			QEN4 FG 4 
			QEN4 HI 3
			QEN4 J 4 
			QEN4 K 3
			QEN4 L 4
			QEN4 M -1
			Stop
		FDeath:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QEN4 N 3
			QEN4 O 4
			QEN4 PQ 3
			QEN4 R 4
			QEN4 S 3
			QEN4 TU 4
			QEN4 V 3
			QEN4 W 4
			QEN4 X -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QEN2 S 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QEN2 S 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QEN2 S 3 A_SetTranslucent(0.9,0)
			QEN2 S 0 A_Scream
			QEN2 S 3 A_SetTranslucent(0.8,0)
			QEN2 S 0 A_NoBlocking
			QEN2 S 3 A_SetTranslucent(0.7,0)
			QEN2 S 3 A_SetTranslucent(0.6,0)
			QEN2 S 3 A_SetTranslucent(0.5,0)
			QEN2 S 3 A_SetTranslucent(0.4,0)
			QEN2 S 3 A_SetTranslucent(0.3,0)
			QEN2 S 3 A_SetTranslucent(0.2,0)
			QEN2 S 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QEN2 S 0 A_GenericFreezeDeath
			QEN2 S 70 A_UnsetSolid
			QEN2 S 0 A_IceGuyDie
			QEN2 S 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("EnforcerHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QEN4 MLKJIHGFEDCBA 3
			QEN3 Z 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QEN1 A 3 A_SetTranslucent(0.2,0)
			QEN1 A 3 A_SetTranslucent(0.3,0)
			QEN1 A 3 A_SetTranslucent(0.4,0)
			QEN1 A 3 A_SetTranslucent(0.5,0)
			QEN1 A 3 A_SetTranslucent(0.6,0)
			QEN1 A 3 A_SetTranslucent(0.7,0)
			QEN1 A 3 A_SetTranslucent(0.8,0)
			QEN1 A 3 A_SetTranslucent(0.9,0)
			QEN1 A 3 A_SetTranslucent(1,0)
			QEN1 A 1 A_SetShootable
			Goto See
	}
}

Actor QEnforcerDG : QEnforcer
{
	Health 80
	GibHealth 35
	WoundHealth -10000
}

ACTOR ATEnforcer
{
	Scale 0.5
	States
	{
		Spawn:
			QEN1 ABCDEFG 4
			TNT1 A 10
			QEN1 HIJKLMNOPQRSTUVW 4
			TNT1 A 10
			QEN1 XYZ 4
			QEN2 ABCDE 4
			TNT1 A 10
			QEN2 FGHIJKLKLMNO 4
			TNT1 A 10
			QEN2 PQRS 4
			TNT1 A 10
			QEN2 TUVWX 4
			TNT1 A 10
			QEN2 YZ 4
			QEN3 ABCDEF 4
			TNT1 A 10
			QEN3 GHIJKLMNOPQRSTUVWXY 4
			TNT1 A 10
			QEN3 Z 4
			QEN4 ABCDEFGHIJKLM 4
			TNT1 A 10
			QEN4 NOPQRSTUVWX 4
			TNT1 A 10
			Loop
	}
}