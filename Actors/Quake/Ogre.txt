ACTOR QOgre
{
	Health 10280
	Radius 16
	Height 56
	Speed 15
	Mass 0x7FFFFFFF
	PainChance 255
	Scale 0.5
	WoundHealth 10080
	MeleeRange 64
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	SeeSound "ogre/sight"
	PainSound "ogre/pain"
	DeathSound "ogre/death"
	ActiveSound "ogre/active"
	Dropitem "DefaultPinataPop"
	DropItem "QuakeRocketBackpack"
	Obituary "%o was destroyed by an Ogre."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QOG1 A 0
			QOG1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QOG1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QOG1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QOG1 A 3 A_Look
			QOG1 B 4 A_Look
			QOG1 C 3 A_Look
			QOG1 D 4 A_Look
			QOG1 E 3 A_Look
			QOG1 F 4 A_Look
			QOG1 G 3 A_Look
			QOG1 H 4 A_Look
			QOG1 I 3 A_Look
			Loop
		Walk:
			TNT1 A 0
			QOG1 J 3 A_Look
			QOG1 K 4 A_Wander
			QOG1 L 3 A_Look
			QOG1 M 3 A_Wander
			QOG1 N 4 A_Look
			QOG1 O 4 A_Wander
			QOG1 P 3 A_Look
			QOG1 Q 4 A_Wander
			QOG1 R 3 A_Look
			QOG1 S 4 A_Wander
			QOG1 T 3 A_Look
			QOG1 U 4 A_Wander
			QOG1 V 3 A_Look
			QOG1 W 4 A_Wander
			QOG1 X 3 A_Look
			QOG1 Y 4 A_Wander
			Loop
		See:
			QOG1 Z 4 A_Chase
			QOG2 A 3 A_Chase
			QOG2 BC 4 A_Chase
			QOG2 DE 3 A_Chase
			QOG2 FG 4 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_Jump(128,"Smash")
		Swing:
			TNT1 A 0 A_PlaySound("ogre/attack", CHAN_WEAPON)
			QOG2 H 4
			QOG2 I 3
			QOG2 JK 4 A_FaceTarget
			QOG2 L 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 M 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 N 4 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 O 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 P 4 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 Q 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG2 R 4 A_CustomMeleeAttack(random(4,12))
			QOG2 S 4 A_FaceTarget
			QOG2 T 3 A_FaceTarget
			QOG2 U 3 A_FaceTarget
			Goto See
		Smash:
			TNT1 A 0 A_PlaySound("ogre/attack", CHAN_WEAPON)
			QOG2 V 4 A_FaceTarget
			QOG2 WX 3 A_FaceTarget
			QOG2 Y 4 A_FaceTarget
			QOG2 Z 3 A_FaceTarget
			QOG3 A 4 A_FaceTarget
			QOG3 B 4 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG3 C 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG3 D 4 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG3 E 3 A_CustomMeleeAttack(random(4,12))
			TNT1 A 0 A_FaceTarget
			QOG3 F 8 A_CustomMeleeAttack(random(4,12))
			QOG3 GH 4 A_FaceTarget
			QOG3 I 3 A_FaceTarget
			Goto See
		Missile:
			QOG3 J 3 A_FaceTarget
			QOG3 K 4 A_FaceTarget
			QOG3 K 3 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/glfire", CHAN_WEAPON)
			QOG3 L 4 A_CustomMissile("OgreGrenade",40,-18,0,CMF_AIMDIRECTION,8)
			QOG3 M 3
			QOG3 N 4
			QOG3 O 3
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_Jump(96,"PainB","PainC","PainD","PainE")
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			TNT1 A 0 A_Pain
			QOG3 P 4
			QOG3 QR 3
			QOG3 S 4
			QOG3 T 3
			TNT1 A 0
			Goto See
		PainB:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			TNT1 A 0 A_Pain
			QOG3 UV 4
			QOG3 W 3
			TNT1 A 0
			Goto See
		PainC:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,35)
			TNT1 A 0 A_Pain
			QOG3 X 4
			QOG3 Y 3
			QOG3 Z 4
			QOG4 AB 3
			QOG4 C 4
			TNT1 A 0
			Goto See
		PainD:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,70)
			TNT1 A 0 A_Pain
			QOG4 D 3
			QOG4 E 4
			QOG4 FG 3
			QOG4 H 4
			QOG4 I 3
			QOG4 JKL 4
			QOG4 MNO 3
			QOG4 PQ 4
			QOG4 R 3
			QOG4 S 4
			TNT1 A 0 
			Goto See
		PainE:
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,70)
			TNT1 A 0 A_Pain
			QOG4 T 3
			QOG4 U 4
			QOG4 V 3
			QOG4 W 4
			QOG4 X 3
			QOG4 Y 4
			QOG4 Z 3
			QOG5 AB 4
			QOG5 CD 3 
			QOG5 E 4
			QOG5 F 3
			QOG5 G 4
			QOG5 H 3
			TNT1 A 0
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,21)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			QOG2 Q 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			QOG2 Q 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 ACS_NamedExecuteAlways("QuakePainCooldown",0,105)
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			QOG2 Q 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			QOG2 Q 30 A_Pain
			QOG2 Q 70
			Goto See
		Wound.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Wound")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		Wound:
			QOG5 I 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeDamage:
		Wound.DukePistol:
			QOG5 I 1 A_JumpIfHealthLower(10000,4)
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.DukeExplosive:
			QOG5 I 1 A_JumpIfHealthLower(10077,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGibDuke")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QOG2 Q 1 A_Die("Zorch")
			Goto Death
		Wound.DukeIce:
			TNT1 A 0 A_Jump(218,2)
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A 0
		Wound.HexenIce:
		Wound.Ice:
			QOG2 Q 1 A_Die("Ice")
			Goto Ice
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			Goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			Goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.Massacre:
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Jump(128,"BDeath")
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QOG5 I 3
			QOG5 J 4
			QOG5 K 3
			QOG5 LM 4
			QOG5 NO 3
			QOG5 P 4
			QOG5 Q 3
			QOG5 R 4
			QOG5 S 3
			QOG5 T 4
			QOG5 U 3
			QOG5 V -1
			Stop
		BDeath:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QOG5 W 3
			QOG5 X 4
			QOG5 Y 3
			QOG5 Z 4
			QOG6 A 3
			QOG6 B 4
			QOG6 C 3
			QOG6 D 4
			QOG6 E 3
			QOG6 F -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QOG3 Q 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QOG3 Q 3 A_SpawnItem("ZorchEffectWide")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QOG3 Q 3 A_SetTranslucent(0.9,0)
			QOG3 Q 0 A_Scream
			QOG3 Q 3 A_SetTranslucent(0.8,0)
			QOG3 Q 0 A_NoBlocking
			QOG3 Q 3 A_SetTranslucent(0.7,0)
			QOG3 Q 3 A_SetTranslucent(0.6,0)
			QOG3 Q 3 A_SetTranslucent(0.5,0)
			QOG3 Q 3 A_SetTranslucent(0.4,0)
			QOG3 Q 3 A_SetTranslucent(0.3,0)
			QOG3 Q 3 A_SetTranslucent(0.2,0)
			QOG3 Q 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QOG3 Q 0 A_GenericFreezeDeath
			QOG3 Q 70 A_UnsetSolid
			QOG3 Q 0 A_IceGuyDie
			QOG3 Q 1 A_IceGuyDie
			TNT1 A -1
			stop
		Death.QuakeGibDuke:
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib",5)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_SpawnItemEx("OgreHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 1 A_PlaySound("quake/gib", CHAN_VOICE)
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QOG5 VUTSRQPONMLKJI 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QOG1 A 3 A_SetTranslucent(0.2,0)
			QOG1 A 3 A_SetTranslucent(0.3,0)
			QOG1 A 3 A_SetTranslucent(0.4,0)
			QOG1 A 3 A_SetTranslucent(0.5,0)
			QOG1 A 3 A_SetTranslucent(0.6,0)
			QOG1 A 3 A_SetTranslucent(0.7,0)
			QOG1 A 3 A_SetTranslucent(0.8,0)
			QOG1 A 3 A_SetTranslucent(0.9,0)
			QOG1 A 3 A_SetTranslucent(1,0)
			QOG1 A 1 A_SetShootable
			Goto See
	}
}

Actor QOgreDG : QOgre
{
	Health 200
	GibHealth 80
	WoundHealth -10000
}

ACTOR ATOgre
{
	Scale 0.5
	States
	{
		Spawn:
			QOG1 ABCDEFGHI 4
			TNT1 A 10
			QOG1 JKLMNOPQRSTUVWXY 4
			TNT1 A 10
			QOG1 Z 4
			QOG2 ABCDEFG 4
			TNT1 A 10
			QOG2 HIJKLMNOPQRSTU 4
			TNT1 A 10
			QOG2 VWXYZ 4
			QOG3 ABCDEFGHI 4
			TNT1 A 10
			QOG3 JKLMNO 4
			TNT1 A 10
			QOG3 PQRST 4
			TNT1 A 10
			QOG3 UVW 4
			TNT1 A 10
			QOG3 XYZ 4
			QOG4 ABC 4
			TNT1 A 10
			QOG4 DEFGHIJKLMNOPQRS 4
			TNT1 A 10
			QOG4 TUVWXYZ 4
			QOG5 ABCDEFGH 4
			TNT1 A 10
			QOG5 IJKLMNOPQRSTUV 4
			TNT1 A 10
			QOG5 WXYZ 4
			QOG6 ABCDEF 4
			TNT1 A 10
			Loop
	}
}