ACTOR Centroid
{
	Health 10315
	Radius 32
	Height 64
	Speed 15
	Mass 0x7FFFFFFF
	PainChance 20
	Scale 0.5
	WoundHealth 10015
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	SeeSound "centroid/sight"
	PainSound "centroid/pain"
	DeathSound "centroid/death"
	ActiveSound "centroid/active"
	Dropitem "DefaultPinataPop"
	Obituary "%o was blasted by a Enforcer."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodType "QuakeBlood"
	States
	{
		Spawn:
			QCT1 A 0
			QCT1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			QCT1 A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			QCT1 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("NoPatrolToken",1,"Stand")
			TNT1 A 0 A_Jump(32,"Walk")
		Stand:
			QCT1 ABCDEFGHIJKL 4 A_Look
			Loop
		Walk:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("centroid/walk")
			QCT1 M 2 A_Wander
			QCT1 M 2 A_Look
			QCT1 N 2 A_Wander
			QCT1 N 2 A_Look
			QCT1 O 2 A_Wander
			QCT1 O 2 A_Look
			QCT1 P 2 A_Wander
			QCT1 P 2 A_Look
			QCT1 Q 2 A_Wander
			QCT1 Q 2 A_Look
			QCT1 R 2 A_Wander
			QCT1 R 2 A_Look
			Loop
		See:
			QCT1 MNOPQR 4 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 S 4 A_CustomMissile("CentroidNail",15,20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 S 4 A_CustomMissile("CentroidNail",15,-20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 T 4 A_CustomMissile("CentroidNail",15,20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 T 4 A_CustomMissile("CentroidNail",15,-20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 S 4 A_CustomMissile("CentroidNail",15,20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 S 4 A_CustomMissile("CentroidNail",15,-20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 T 4 A_CustomMissile("CentroidNail",15,20)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_PlaySound("quakeweps/nailgunfire", CHAN_WEAPON)
			QCT1 T 4 A_CustomMissile("CentroidNail",15,-20)
			Goto See
		Melee:
			QCT1 UVWXYZ 4 A_FaceTarget
			QCT2 A 4 A_CustomMeleeAttack(90,"shambler/smack","centroid/attack")
			QCT2 BCDE 4 A_FaceTarget
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			QCT2 F 3 A_Pain
			QCT2 GHIJ 3
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 
			Goto See
		Wound:
			QCT2 K 1 A_JumpIfHealthLower(10000,2)
			TNT1 A 0 A_Die
			TNT1 A 0 A_Die("QuakeGib")
			Goto Death
		Wound.ZorchPistol:
		Wound.Zorch:
		Wound.ZorchKamikaze:
		Wound.PhasingZorch:
		Wound.ZorchSpectral:
			QGR3 L 1 A_Die("Zorch")
			Goto Death
		Death:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			QCT2 KLMNO 3
			QCT2 O -1 A_KillMaster
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			QCT2 F 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			QCT2 F 3 A_SpawnItem("ZorchEffectWide")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			QCT2 F 3 A_SetTranslucent(0.9,0)
			QCT2 F 0 A_Scream
			QCT2 F 3 A_SetTranslucent(0.8,0)
			QCT2 F 0 A_NoBlocking
			QCT2 F 3 A_SetTranslucent(0.7,0)
			QCT2 F 3 A_SetTranslucent(0.6,0)
			QCT2 F 3 A_SetTranslucent(0.5,0)
			QCT2 F 3 A_SetTranslucent(0.4,0)
			QCT2 F 3 A_SetTranslucent(0.3,0)
			QCT2 F 3 A_SetTranslucent(0.2,0)
			QCT2 F 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.QuakeGib:
		XDeath:
			TNT1 A 1 A_GiveToTarget("KillCount",1)
			TNT1 A 0 DamageThing(20000)
			TNT1 A 0 A_PlaySound("quake/gib", CHAN_VOICE)
			TNT1 A 0 A_SpawnItemEx("CentroidHead",0,0,0,random(-8,8),0,0,random(1,359),1)
			TNT1 A 0 A_SpawnItemEx("QuakeGibDropper",0,0,random(20,40),0,0,0,0,1)
			TNT1 A 0 A_KillMaster
			Stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			QCT2 ONMLK 3
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			QCT1 A 3 A_SetTranslucent(0.2,0)
			QCT1 A 3 A_SetTranslucent(0.3,0)
			QCT1 A 3 A_SetTranslucent(0.4,0)
			QCT1 A 3 A_SetTranslucent(0.5,0)
			QCT1 A 3 A_SetTranslucent(0.6,0)
			QCT1 A 3 A_SetTranslucent(0.7,0)
			QCT1 A 3 A_SetTranslucent(0.8,0)
			QCT1 A 3 A_SetTranslucent(0.9,0)
			QCT1 A 3 A_SetTranslucent(1,0)
			QCT1 A 1 A_SetShootable
			Goto See
	}
}

Actor CentroidDG : Centroid
{
	Health 300
	GibHealth 15
	WoundHealth -10000
}

ACTOR ATCentroid
{
	Scale 0.5
	States
	{
		Spawn:
			QCT1 ABCDEFGHIJKL 4
			TNT1 A 10
			QCT1 MNOPQR 4
			TNT1 A 10
			QCT1 STSTSTST 4
			TNT1 A 10
			QCT1 UVWXYZ 4
			QCT2 ABCDE 4
			TNT1 A 10
			QCT2 FGHIJ 4
			TNT1 A 10
			QCT2 KLMNO 4
			TNT1 A 10
			Loop
	}
}