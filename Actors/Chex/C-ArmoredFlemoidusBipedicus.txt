actor ArmoredFlemoidusBipedicus2 : ArmoredFlemoidusBipedicusV3 replaces ArmoredFlemoidusBipedicusV3
{
	SeeSound "cqimp/sight"
	PainSound "cqimp/pain"
	DeathSound "cqimp/death"
	ActiveSound "cqimp/active"
    Bloodcolor "Green"
    States
    {
      Spawn:
        TROC AB 10 A_Look
        Loop

      See:
        TROC AABBCCDD 3 A_Chase
        Loop

      Melee:
      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        TROC EF 6 A_FaceTarget // Previously 8 each
        TROC G 6 A_CustomComboAttack("BipedicusBallV3", 32, random[TROCpattack](1,8)*3, "imp/melee", "flem")
        Goto See

      Pain:
        TROC A 2 A_SpawnDebris("FlemoidGib",1)
        TROC A 2 A_Pain
        Goto See

      Pain.ZorchPistol:

      Pain.Zorch:
        TROC H 2
        TROC H 2 A_Pain
        Goto See

      Death.ZorchPistol:
	  
      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TROC I 8
        TROC J 8 A_Scream
        TROC K 6
        TROC L 6 A_NoBlocking
        TROC M -1
        Stop

      Death.DukePistol:
	  
      Death.DukeExplosive:
	  
      Death.DukeDamage:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        TNT1 A 0 A_SpawnItem("ArmoredBipedicusBody")
        //SPOD A 10
        //SPOD ABC 7
        //SPOD D -1
		TNT1 A -1
        stop

      Raise:
        TROC MLKJI 8
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TROC A 0 A_UnsetSolid
        TROC V 70 A_GenericFreezeDeath
        TROC A 0 A_IceGuyDie
        TROC V 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TROC A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        TROC A 16 A_Pain
        Goto See
    }
}