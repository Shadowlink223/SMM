actor NewLarva : Larva
{
	SeeSound "cqspectre/sight"
	AttackSound "cqspectre/melee"
	PainSound "cqspectre/pain"
	DeathSound "cqspectre/death"
	ActiveSound "cqspectre/active"
	DropItem "DefaultPinataPop"
    Bloodcolor "Green"
    States
    {
		Spawn:
			LRVA A 0
			LRVA A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			LRVA A 1 ACS_ExecuteAlways(668,0,1,0)
			LRVA A 0 A_GiveInventory("AlreadySpawned",1)
			LRVA A 0 A_JumpIfInventory("InvisibleToken",1,2)
			LRVA A 0 A_Jump(256,2)
			LRVA A 0 A_ChangeFlag("STEALTH",1)
      Spawn2:
        LRVA AB 10 A_Look
        Loop

      See:
        LRVA AABBAABB 2 A_Chase
        Loop

      Melee: // Previously 8 frames each
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        LRVA CD 6 A_FaceTarget
        LRVA E 6 A_CustomMeleeAttack(random[SARCattack](1,10)*4, "", "", "Flem")
        Goto See

      Pain.ZorchPistol:
      Pain.Zorch:
	  Pain.PhasingZorch:
	  Pain.ZorchKamikaze:
        LRVA F 2
        LRVA F 2 A_Pain
        Goto See

      Pain:
        LRVA A 2 A_SpawnDebris("FlemoidGib",1)
        LRVA A 2 A_Pain
        Goto See

      Death.ZorchPistol:
      Death.Zorch:
      Death.PhasingZorch:
	  Death.ZorchKamikaze:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        LRVA G 8
        LRVA H 8 A_Scream
        LRVA I 4
        LRVA J 4 A_NoBlocking
        LRVA K 4
        LRVA L -1
        Stop

      Death.DukeExplosive:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        LRVD A 10
        LRVD ABC 7
        LRVD D -1
		TNT1 A -1
        stop

      Raise:
        LRVA L 5
        LRVA KJIHG 5
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("LarvaFrozen")
        LRVA A 0 A_UnsetSolid
        LRVA M 70 A_GenericFreezeDeath
        LRVA A 0 A_IceGuyDie
        LRVA M 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        LRVA A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        LRVA A 16 A_Pain
        Goto See
    }
}

ACTOR OldLarva : NewLarva
{
	States
	{
		Melee:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        LRVA CD 8 A_FaceTarget
        LRVA E 8 A_CustomMeleeAttack(random[SARCattack](1,10)*4, "", "", "Flem")
        Goto See
	}
}

ACTOR MultiOldLarva : OldLarva
{
	Species "Larva"
	+THRUSPECIES
}

ACTOR MultiNewLarva : NewLarva
{
	Species "Larva"
	+THRUSPECIES
}