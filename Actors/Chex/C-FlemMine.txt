actor FlemMine2 : FlemMine replaces FlemMine
{
	SeeSound "cqskull/sight"
	AttackSound "cqskull/melee"
	PainSound "cqskull/pain"
	DeathSound "cqskull/death"
	ActiveSound "cqskull/active"
    Bloodcolor "Green"
    States
    {
      Spawn:
        SKUC AB 10 A_Look
        Loop

      See:
        SKUC AB 6 A_Chase
        Loop

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        SKUC C 10 A_FaceTarget
        SKUC D 4 A_SkullAttack
        SKUC CD 4 
        Goto Missile+2

      Pain:
        SKUC A 3 A_SpawnDebris("FlemoidGib") 
        SKUC A 3 A_Pain
        Goto See

      Pain.ZorchPistol:

      Pain.Zorch:
        SKUC E 3 
        SKUC E 3 A_Pain
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        SKUC F 6 
        SKUC G 6 A_Scream
        SKUC H 6 
        SKUC I 6 A_NoBlocking
        SKUC J 6
        SKUC K 6
        Stop

      Death.DukeExplosive:

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        TNT1 A 0 A_SpawnDebris("FlemMineGib1",1)
        TNT1 A 0 A_SpawnDebris("FlemMineGib2",1)
        TNT1 A -1
        stop

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("FlemMineFrozen")
        SKUC A 0 A_UnsetSolid
        SKUC L 70 A_GenericFreezeDeath
        SKUC A 0 A_IceGuyDie
        SKUC L 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        SKUC A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        SKUC A 16 A_Pain
        Goto See
    }
}