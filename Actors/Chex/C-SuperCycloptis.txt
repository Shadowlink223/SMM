actor SuperCycloptis2 : SuperCycloptis replaces SuperCycloptis
{
	SeeSound "cqcaco/sight"
	PainSound "cqcaco/pain"
	DeathSound "cqcaco/death"
	ActiveSound "cqcaco/active"
    Bloodcolor "Green"
    States
    {
      Spawn:
        HEAC A 10 A_Look
        Loop

      See:
        HEAC A 3 A_Chase
        Loop

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        HEAC B 5 A_FaceTarget
        HEAC C 5 A_FaceTarget
        HEAC D 5 A_CustomComboAttack("CycloptisBall", 32, random[HEACattack](1,6)*10, "", "flem")
        Goto See

      Pain:
        HEAC A 3 A_SpawnDebris("FlemoidGib",1)
        HEAC A 3 A_Pain
        HEAC A 6
        Goto See

      Pain.ZorchPistol:

      Pain.Zorch:
        HEAC E 3
        HEAC E 3 A_Pain
        HEAC F 6
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        HEAC G 8
        HEAC H 8 A_Scream
        HEAC I 8
        HEAC J 8
        HEAC K 8 A_NoBlocking
        HEAC L -1 A_SetFloorClip
        Stop

      Death.DukeExplosive:

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        TNT1 A 0 A_CustomMissile("SuperCyclopsBody",0,0,random(-180,180))
        TNT1 A 0 A_CustomMissile("SuperCyclopsArm1",0,0,random(-45,-135),2,random(0,90))
        TNT1 A 0 A_CustomMissile("SuperCyclopsArm2",0,0,random(45,135),2,random(0,90))
        TNT1 A -1 A_SetFloorClip
        stop

      Raise:
        HEAC L 8 A_UnSetFloorClip
        HEAC KJIHG 8
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        HEAC A 0 A_UnsetSolid
        HEAC M 70 A_GenericFreezeDeath
        HEAC A 0 A_IceGuyDie
        HEAC M 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        HEAC A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        HEAC A 16 A_Pain
        Goto See
    }
}