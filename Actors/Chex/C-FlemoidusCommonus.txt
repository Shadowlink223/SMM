actor FlemoidusCommonus3 : FlemoidusCommonus2 replaces FlemoidusCommonus2
{
	SeeSound "cqgrunt/sight"
	AttackSound "cqgrunt/attack"
	PainSound "cqgrunt/pain"
	DeathSound "cqgrunt/death"
	ActiveSound "cqgrunt/active"
    Bloodcolor "Green"
    States
    {
	  Spawn:
		POSC AB 10 A_Look
		Loop
	  See:
		POSC AABBCCDD 4 A_Chase
		Loop
	  Missile:
		stop
	  Melee:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		POSC E 10 A_FaceTarget
		POSC E 0 A_PlaySound ("cqgrunt/attack")
		POSC F 8 A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "FlemPuff", 0)
		POSC E 8
		Goto See
	  Raise:
		POSC KJIH 5
		Goto See
	
      Pain:
        POSC A 3 A_SpawnDebris("FlemoidGib",1)
        POSC A 3 A_Pain
        Goto See

      Pain.ZorchPistol:

      Pain.Zorch:
        POSC G 3
        POSC G 3 A_Pain
        Goto See

      Death.DukeExplosive:

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        //POSD A 10
        //POSD ABC 7
        //POSD D -1
		TNT1 A -1
        stop

      Death.ZorchPistol:

      Death.Zorch:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        POSC H 5
        POSC I 5 A_Scream
        POSC J 5 A_NoBlocking
        POSC K 5
        POSC L -1
        Stop

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        POSC A 0 A_UnsetSolid
        POSC V 70 A_GenericFreezeDeath
        POSC A 0 A_IceGuyDie
        POSC V 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        POSC A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        POSC A 16 A_Pain
        Goto See
    }
}