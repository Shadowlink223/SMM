ACTOR Flembomination2
{
	Health 3000
	Radius 100
	Height 100
	Mass 1000
	damagefactor "BFG", 0.75
    damagefactor "BFGSpray", 0.75
    damagefactor "Flem", 0.0
    damagefactor "Slime", 0.0  //in chex3.exe the slam now has slime damagetype
    PainChance "MarathonFusion", 0
	Speed 12
	PainChance 40
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	+DONTGIB
    +NOTDMATCH
    +NOTARGET
    +NOINFIGHTING
    //+NOBLOODDECALS
	SeeSound "cqspider/sight"
	AttackSound "cqspider/attack"
	PainSound "cqspider/pain"
	DeathSound "cqspider/death"
	ActiveSound "cqspider/active"
	DropItem "DefaultPinataPop"
	DropItem "DefaultPinataPop"
	Obituary "$OB_FLEMBOMINATION"
    Bloodcolor "Green"
	States
	{
		Spawn:
			SPIC A 0
			SPIC A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SPIC A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			SPIC A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		SPIC ABCD 6 A_Look
		Loop
	See:
        TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
        TNT1 A 0 A_GiveInventory("DukeBossSee",1)
        TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
    See2:
		SPIC A 3 A_PlaySoundEx("cqcyber/hoof","SoundSlot5",0,2)
		SPIC ABBCC 3 A_Chase
		SPIC D 3 A_PlaySoundEx("cqspider/walk","SoundSlot5",0,2)
		SPIC D 3 A_Chase
		Loop
	Missile:
		SPIC F 20 A_FaceTarget 
		SPIC G 8 A_CustomComboAttack("CycloptisBall2", 32, random[headattack](1,6)*10, "", "flem")
 		SPIC H 8 A_CustomComboAttack("CycloptisBall2", 32, random[headattack](1,6)*10, "", "flem")
		Goto Missile
	Pain:
        TNT1 AAAA 0 A_SpawnDebris("FlemoidGib",1)
		SPIC I 3
		SPIC I 3 A_Pain
		Goto See
	Pain.Zorch:
	Pain.ZorchPistol:
	Pain.PhasingZorch:
	Pain.ZorchKamikaze:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		SPIC I 3
		TNT1 A 0 A_CustomMissile("FlemSporeSummonball",0,0,0)
		TNT1 A 0 A_CustomMissile("FlemSporeSummonball",0,0,0)
		SPIC I 3 A_Pain
		Goto See
	Death.Zorch:
	Death.ZorchPistol:
	Death.PhasingZorch:
	Death.ZorchKamikaze:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		SPIC J 20 A_Scream
		SPIC K 10 A_NoBlocking
		SPIC LMNOPQR 10
		SPIC S 30
		SPIC S -1 A_KillMaster
		Stop
	Death.DukeDamage:
	Death.MightyBoot:
	Death.DukeExplosive:
	Death.DukeIce:
	Death.DukePistol:
		TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
	Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("FlemoidGib",random(-1,1),random(-1,1),random(46,56),random(-2,2),random(-2,2),random(5,15))
		TNT1 A -1 A_KillMaster
		Stop
	}
}

ACTOR MultiFlembomination2 : Flembomination2
{
	Species "Flembomination"
	+THRUSPECIES
}