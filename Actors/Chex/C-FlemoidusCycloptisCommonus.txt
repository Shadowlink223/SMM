actor NewFlemoidusCycloptisCommonus : FlemoidusCycloptisCommonusV3
{
	SeeSound "cqspectre/sight"
	AttackSound "cqspectre/melee"
	PainSound "cqspectre/pain"
	DeathSound "cqspectre/death"
	ActiveSound "cqspectre/active"
	DropItem "DefaultPinataPop"
    Bloodcolor "Green"
    States
    {
		Spawn:
			CYCL A 0
			CYCL A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			CYCL A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			CYCL A 0 A_GiveInventory("AlreadySpawned",1)
      Spawn2:
        CYCL AB 10 A_Look
        Loop

      See:
        CYCL AABBCCDD 2 A_Chase
        Loop

      Melee: // Previously all 8 frames each
        CYCL EF 6 A_FaceTarget
        CYCL G 6 A_CustomMeleeAttack(random[SARCattack](1,10)*4, "", "", "Flem")
        Goto See

      Pain:
        CYCL A 2 A_SpawnDebris("FlemoidGib",1)
        CYCL A 2 A_Pain
        Goto See

      Pain.ZorchPistol:
      Pain.Zorch:
	  Pain.PhasingZorch:
	  Pain.ZorchKamikaze:
        CYCL H 2
        CYCL H 2 A_Pain
        Goto See

      Death.ZorchPistol:
      Death.Zorch:
      Death.PhasingZorch:
	  Death.ZorchKamikaze:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CYCL I 8
        CYCL J 8 A_Scream
        CYCL K 4
        CYCL L 4 A_NoBlocking
        CYCL M 4
        CYCL N -1
        Stop

      Death.DukeExplosive:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        TNT1 A 0 A_SpawnItemEx("CyclopsBody",0,0,25,random(-1,1),random(-1,1),random(-1,4))//A_CustomMissile("CyclopsBody",15,0,random(-180,180))//A_SpawnItem("CyclopsBody")
        TNT1 AAA 0 A_CustomMissile("CyclopsArm",0,0,random(-180,180),2,random(0,90))//A_SpawnDebris("CyclopsArm",1)
        TNT1 A -1
        stop

      Raise:
        CYCL N 5
        CYCL MLKJI 5
        Goto See

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice

      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        CYCL A 0 A_UnsetSolid
        CYCL O 70 A_GenericFreezeDeath
        CYCL A 0 A_IceGuyDie
        CYCL O 1 A_IceGuyDie
        TNT1 A -1
        stop

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        CYCL A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        CYCL A 16 A_Pain
        Goto See
    }
}

ACTOR OldFlemoidusCycloptisCommonus : NewFlemoidusCycloptisCommonus
{
	Health 150
	-FLOAT
    -NOGRAVITY
	States
	{
		Melee:
        CYCL EF 8 A_FaceTarget
        CYCL G 8 A_CustomMeleeAttack(random[SARCattack](1,10)*4, "", "", "Flem")
        Goto See
	}
}

ACTOR MultiNewFlemoidusCycloptisCommonus : NewFlemoidusCycloptisCommonus
{
	Species "FlemoidCyclop"
	+THRUSPECIES
}

ACTOR MultiOldFlemoidusCycloptisCommonus : OldFlemoidusCycloptisCommonus
{
	Species "FlemoidCyclop"
	+THRUSPECIES
}