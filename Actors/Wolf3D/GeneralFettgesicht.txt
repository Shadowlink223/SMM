ACTOR GeneralFettgesicht
{
	Health 1200
	Radius 33
	Height 70
	Speed 6
	Mass 1000
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+BOSS
	+NORADIUSDMG
	+NOPAIN
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+LOOKALLAROUND
	SeeSound "fettgesicht/sight"
	PainSound ""
	DeathSound "fettgesicht/death"
	ActiveSound ""
	AttackSound "boss/fire"
	Dropitem "DefaultPinataPop"
	Dropitem "DefaultPinataPop"
	Obituary "%o was shot by General Fettgesicht."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	Decal BulletChip
	States
	{
		Spawn:
			WFET A 0
			WFET A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			WFET A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			WFET A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			WFET A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TNT1 A 0 A_GiveInventory("DukeBossSee",1)
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
		See2:
			WFET AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			WFET EEEEE 3 A_FaceTarget
			WFET F 5 Bright
			TNT1 A 0 A_CustomMissile("FettWolfRocket",32,16,0,8)
			WFET GHGH 5 Bright A_WolfAttack(0,"wolfen/missilefire",0.666,64,32,2,4,160.0)
			Goto See
		Death.DukeDamage:
		Death.MightyBoot:
		Death.DukeExplosive:
		Death.DukeIce:
		Death.DukePistol:
			TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SMMCVarCheck",2) == 1, "FastDeath")
			WFET A 70 A_Scream
			TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
			WFET I 5
			WFET J 5
			WFET K 5 A_NoBlocking
			WFET L 30
			WFET L -1 A_KillMaster
			Stop
		FastDeath:
			WFET A 5 A_Scream
			Goto XDeath+3
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WFET D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WFET D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WFET D 3 A_SetTranslucent(0.9,0)
			WFET D 0 A_Scream
			WFET D 3 A_SetTranslucent(0.8,0)
			WFET D 0 A_NoBlocking
			WFET D 3 A_SetTranslucent(0.7,0)
			WFET D 3 A_SetTranslucent(0.6,0)
			WFET D 3 A_SetTranslucent(0.5,0)
			WFET D 3 A_SetTranslucent(0.4,0)
			WFET D 3 A_SetTranslucent(0.3,0)
			WFET D 3 A_SetTranslucent(0.2,0)
			WFET D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		DeathHappyfun:
			TNT1 A 0
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAA 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
			Goto XDeath+4
		DeathBrutal:
			TNT1 A 0
			TNT1 AAAAAAAA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAAA 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
			TNT1 AA 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160)
			Goto XDeath+4
	}
}

ACTOR MultiGeneralFettgesicht : GeneralFettgesicht
{
	+THRUSPECIES
}