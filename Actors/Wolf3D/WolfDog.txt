ACTOR WolfDog 
{
	Health 1
	Radius 12
	Height 32
	Speed 12
	PainChance 255
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	SeeSound "dog/sight"
	PainSound ""
	DeathSound "dog/death"
	ActiveSound ""
	AttackSound ""
	Dropitem "DefaultPinataPop"
	Obituary "%o was bitten by a dog."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States 
	{
		Spawn:
			WDOG A 0
			WDOG A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			WDOG A 1 ACS_ExecuteAlways(668,0,1,0)
			WDOG A 0 A_GiveInventory("AlreadySpawned",1)
			WDOG A 0 A_JumpIfInventory("InvisibleToken",1,2)
			WDOG A 0 A_Jump(256,2)
			WDOG A 0 A_ChangeFlag("STEALTH",1)
		Spawn2:
			WDOG A 0 A_Look
			WDOG A 2 A_Wander
			WDOG A 0 A_Look
			WDOG A 2 A_Wander
			WDOG A 0 A_Look
			WDOG A 2 A_Wander
			WDOG B 0 A_Look
			WDOG B 2 A_Wander
			WDOG B 0 A_Look
			WDOG B 2 A_Wander
			WDOG C 0 A_Look
			WDOG C 2 A_Wander
			WDOG C 0 A_Look
			WDOG C 2 A_Wander
			WDOG C 0 A_Look
			WDOG C 2 A_Wander
			WDOG D 0 A_Look
			WDOG D 2 A_Wander
			WDOG D 0 A_Look
			WDOG D 2 A_Wander
			Loop
		See:
			WDOG AAABBCCCDD 2 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WDOG E 5 A_FaceTarget
			WDOG F 6 A_FaceTarget
			WDOG G 4 A_CustomMeleeAttack(random(1,16),"dog/bite","dog/bite")
			WDOG E 5
			WDOG A 5
			Goto See
		Pain:
			WDOG F 3
			WDOG F 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			WDOG H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			WDOG H 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			WDOG A 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			WDOG A 30 A_Pain
			WDOG A 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
			WDOG H 7
			WDOG I 8 A_Scream
			WDOG J 7 A_NoBlocking
			WDOG K -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WDOG H 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WDOG H 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WDOG H 3 A_SetTranslucent(0.9,0)
			WDOG H 0 A_Scream
			WDOG H 3 A_SetTranslucent(0.8,0)
			WDOG H 0 A_NoBlocking
			WDOG H 3 A_SetTranslucent(0.7,0)
			WDOG H 3 A_SetTranslucent(0.6,0)
			WDOG H 3 A_SetTranslucent(0.5,0)
			WDOG H 3 A_SetTranslucent(0.4,0)
			WDOG H 3 A_SetTranslucent(0.3,0)
			WDOG H 3 A_SetTranslucent(0.2,0)
			WDOG H 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("DogFrozen")
			WDOG L 0 A_GenericFreezeDeath
			WDOG L 70 A_UnsetSolid
			WDOG L 0 A_IceGuyDie
			WDOG L 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			WDOG KJIH 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			WDOG A 3 A_SetTranslucent(0.2,0)
			WDOG A 3 A_SetTranslucent(0.3,0)
			WDOG A 3 A_SetTranslucent(0.4,0)
			WDOG A 3 A_SetTranslucent(0.5,0)
			WDOG A 3 A_SetTranslucent(0.6,0)
			WDOG A 3 A_SetTranslucent(0.7,0)
			WDOG A 3 A_SetTranslucent(0.8,0)
			WDOG A 3 A_SetTranslucent(0.9,0)
			WDOG A 3 A_SetTranslucent(1,0)
			WDOG A 1 A_SetShootable
			Goto See
		DeathHappyfun:
			TNT1 A 0
			TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
			TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
			Goto XDeath+2
		DeathBrutal:
			TNT1 A 0
			TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160)
			Goto XDeath+2
	}
}

ACTOR MultiWolfDog : WolfDog
{
	+THRUSPECIES
}

ACTOR WolfDogSpectre : WolfDog
{
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
}

ACTOR MultiWolfDogSpectre : WolfDogSpectre
{
	+THRUSPECIES
}