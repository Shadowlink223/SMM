ACTOR SODSpectre
{
	Health 25
	Radius 16	
	Height 50
	DeathHeight 50
	Speed 2
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	+LOOKALLAROUND
	SeeSound "wolfspectre/sight"
	DeathSound "wolfspectre/death"
	Dropitem "DefaultPinataPop"
	Obituary "%o had their soul taken by a spectre."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	DamageFactor "WolfEnemyBullet", 0.0
	States 
	{
		Spawn:
			SSPE A 0
			SSPE A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SSPE A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			SSPE A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			SSPE ABCD 5 A_Look
			loop
		See:
			SSPE AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Melee:
			SSPE A 1 A_CustomMeleeAttack(3,0,0,0,0)
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			SSPE F 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			SSPE A 30 A_Pain
			SSPE A 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death:
		XDeath:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SMMCVarCheck",11) == 0, "Dormant")
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SSPE E 5 A_Scream
			SSPE FG 5 A_NoBlocking
			SSPE H 5
			TNT1 A 0
			Stop
		Dormant:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_Scream
			SSPE E 5 A_UnsetShootable
			SSPE F 5 HealThing(500)
			SSPE G 5
			SSPE H 155 A_UnsetSolid
			TNT1 A 0 A_SetSolid
			TNT1 A 0 A_SetShootable
			Goto See
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SSPE A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			SSPE A 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			SSPE A 3 A_SetTranslucent(0.9,0)
			SSPE A 0 A_Scream
			SSPE A 3 A_SetTranslucent(0.8,0)
			SSPE A 0 A_NoBlocking
			SSPE A 3 A_SetTranslucent(0.7,0)
			SSPE A 3 A_SetTranslucent(0.6,0)
			SSPE A 3 A_SetTranslucent(0.5,0)
			SSPE A 3 A_SetTranslucent(0.4,0)
			SSPE A 3 A_SetTranslucent(0.3,0)
			SSPE A 3 A_SetTranslucent(0.2,0)
			SSPE A 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
	}
}

ACTOR MultiSODSpectre : SODSpectre
{
	+THRUSPECIES
}