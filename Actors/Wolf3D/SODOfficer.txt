ACTOR SODOfficer 
{
	Health 50
	Radius 16
	Height 50
	Speed 10
	PainChance 170
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	SeeSound "sofficer/sight"
	PainSound ""
	DeathSound "sofficer/death"
	ActiveSound ""
	AttackSound "nazi/fire"
	Obituary "%o was shot by a nazi special officer."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	Dropitem "SODClip"
	Decal BulletChip
	States 
	{
		Spawn:
			SOFF A 1 A_Look
			Loop
		See:
			SOFF BBCCDDEE 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SOFF G 3 A_FaceTarget
			SOFF H 10 A_FaceTarget
			TNT1 A 0 A_JumpIfCloser (128, "Bullet1")
			TNT1 A 0 A_JumpIfCloser (256, "Bullet2")
			TNT1 A 0 A_JumpIfCloser (384, "Bullet3")
			TNT1 A 0 A_JumpIfCloser (512, "Bullet4")
			TNT1 A 0 A_JumpIfCloser (640, "Bullet5")
			TNT1 A 0 A_JumpIfCloser (768, "Bullet6")
			Goto Bullet7
		Bullet1:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD1",32,0,random(-6,6),8)
			Goto MissileEnd
		Bullet2:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD2",32,0,random(-7,7),8)
			Goto MissileEnd
		Bullet3:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD3",32,0,random(-8,8),8)
			Goto MissileEnd
		Bullet4:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD4",32,0,random(-9,9),8)
			Goto MissileEnd
		Bullet5:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD5",32,0,random(-10,10),8)
			Goto MissileEnd
		Bullet6:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD6",32,0,random(-11,11),8)
			Goto MissileEnd
		Bullet7:
			SOFF I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			SOFF I 4 Bright A_CustomMissile("WolfEnemyBulletD7",32,0,random(-12,12),8)
			Goto MissileEnd
		MissileEnd:
			SOFF I 1
			Goto See
		Pain:
			SOFF F 3
			SOFF F 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			SOFF F 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			SOFF F 16 A_Pain
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SOFF K 5 A_Scream
			SOFF L 6 A_NoBlocking
			SOFF MN 5 
			SOFF O -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			SOFF F 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			SOFF F 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			SOFF F 3 A_SetTranslucent(0.9,0)
			SOFF F 0 A_Scream
			SOFF F 3 A_SetTranslucent(0.8,0)
			SOFF F 0 A_NoBlocking
			SOFF F 3 A_SetTranslucent(0.7,0)
			SOFF F 3 A_SetTranslucent(0.6,0)
			SOFF F 3 A_SetTranslucent(0.5,0)
			SOFF F 3 A_SetTranslucent(0.4,0)
			SOFF F 3 A_SetTranslucent(0.3,0)
			SOFF F 3 A_SetTranslucent(0.2,0)
			SOFF F 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("OfficerFrozen")
			SOFF P 0 A_GenericFreezeDeath
			SOFF P 70 A_UnsetSolid
			SOFF P 0 A_IceGuyDie
			SOFF P 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			SOFF NMLK 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			SOFF A 3 A_SetTranslucent(0.2,0)
			SOFF A 3 A_SetTranslucent(0.3,0)
			SOFF A 3 A_SetTranslucent(0.4,0)
			SOFF A 3 A_SetTranslucent(0.5,0)
			SOFF A 3 A_SetTranslucent(0.6,0)
			SOFF A 3 A_SetTranslucent(0.7,0)
			SOFF A 3 A_SetTranslucent(0.8,0)
			SOFF A 3 A_SetTranslucent(0.9,0)
			SOFF A 3 A_SetTranslucent(1,0)
			SOFF A 1 A_SetShootable
			Goto See
	}
}