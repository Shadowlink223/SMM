ACTOR WolfMutant 
{
	Health 65
	Radius 16
	Height 48
	Speed 6
	PainChance 170
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	SeeSound ""
	PainSound ""
	DeathSound "mutant/death"
	ActiveSound ""
	AttackSound "nazi/fire"
	Dropitem "DefaultPinataPop"
	Obituary "%o was shot by a mutant."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Dropitem "WolfClip"
	Decal BulletChip
	Bloodcolor "c8 00 cc"
	States 
	{
		Spawn:
			WMUT A 0
			WMUT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			WMUT A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			WMUT A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			WMUT A 1 A_Look
			Loop
		See:
			WMUT BBBCCDDDEE 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIfInventory("NoPainToken",0,2)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WMUT G 3 A_FaceTarget
			WMUT H 10 bright A_WolfAttack(0,"nazi/fire",1.0,64,32,2,4,160.0)
			WMUT I 5 A_FaceTarget
			WMUT J 10 bright A_WolfAttack(0,"nazi/fire",1.0,64,32,2,4,160.0)
			TNT1 A 0 A_JumpIfCloser(80,1)
			Goto See
			TNT1 A 0
			Goto Missile
		Pain:
			WMUT F 3
			WMUT F 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			WMUT F 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			WMUT F 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			WMUT F 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			WMUT F 30 A_Pain
			WMUT F 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("NewDukeGibs1_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs2_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs3_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs4_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs5_WolfPurple")
			TNT1 A 0 A_SpawnDebris("NewDukeGibs6_WolfPurple")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
			WMUT L 2 
			WMUT M 3 A_Scream
			WMUT N 4 A_NoBlocking
			WMUT O 3
			WMUT P -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WMUT F 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WMUT F 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WMUT F 3 A_SetTranslucent(0.9,0)
			WMUT F 0 A_Scream
			WMUT F 3 A_SetTranslucent(0.8,0)
			WMUT F 0 A_NoBlocking
			WMUT F 3 A_SetTranslucent(0.7,0)
			WMUT F 3 A_SetTranslucent(0.6,0)
			WMUT F 3 A_SetTranslucent(0.5,0)
			WMUT F 3 A_SetTranslucent(0.4,0)
			WMUT F 3 A_SetTranslucent(0.3,0)
			WMUT F 3 A_SetTranslucent(0.2,0)
			WMUT F 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			WMUT Q 0 A_GenericFreezeDeath
			WMUT Q 70 A_UnsetSolid
			WMUT Q 0 A_IceGuyDie
			WMUT Q 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			WMUT PONML 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			WMUT A 3 A_SetTranslucent(0.2,0)
			WMUT A 3 A_SetTranslucent(0.3,0)
			WMUT A 3 A_SetTranslucent(0.4,0)
			WMUT A 3 A_SetTranslucent(0.5,0)
			WMUT A 3 A_SetTranslucent(0.6,0)
			WMUT A 3 A_SetTranslucent(0.7,0)
			WMUT A 3 A_SetTranslucent(0.8,0)
			WMUT A 3 A_SetTranslucent(0.9,0)
			WMUT A 3 A_SetTranslucent(1,0)
			WMUT A 1 A_SetShootable
			Goto See
		DeathHappyfun:
			TNT1 A 0
			TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
			TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
			Goto XDeath+2
		DeathBrutal:
			TNT1 A 0
			TNT1 AA 0 A_SpawnItemEx("PurpleMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier_Purple",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny_Purple",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("PurpleMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood_Purple",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160)
			Goto XDeath+2
	}
}

ACTOR MultiWolfMutant : WolfMutant
{
	+THRUSPECIES
}