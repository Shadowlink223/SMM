ACTOR DoctorSchabbs
{
	Health 2400
	Radius 33
	Height 70
	Speed 6
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+BOSS
	+NORADIUSDMG
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+LOOKALLAROUND
	SeeSound "schabbs/sight"
	PainSound ""
	DeathSound "schabbs/death"
	ActiveSound ""
	AttackSound "schabbs/throw"
	Dropitem "DefaultPinataPop"
	Dropitem "DefaultPinataPop"
	Obituary "%o was injected with Doctor Schabb's serum."
    DamageFactor "Mace", 1.15
    DamageFactor "DragonClaw", 1.6
    DamageFactor "MarathonFusion", 2.0
    DamageFactor "WMCNade", 0.75
	DamageFactor "WolfEnemyBullet", 0.0
	States
	{
		Spawn:
			WSCB A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TNT1 A 0 A_GiveInventory("DukeBossSee",1)
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
		See2:
			WSCB AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WSCB EEEEE 3 A_FaceTarget
			WSCB F 6
			TNT1 A 0 A_CustomMissile("WolfSyringe",32,16,0,8)
			Goto See
		Pain:
			WSCB D 3
			WSCB D 3 A_Pain
			Goto See
		Death.DukeDamage:
		Death.MightyBoot:
		Death.DukeExplosive:
		Death.DukeIce:
		Death.DukePistol:
			TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,2) == 1, "FastDeath")
			WSCB A 66 A_Scream
			WSCB G 5
			WSCB H 5
			WSCB I 5 A_NoBlocking
			WSCB J 30
			WSCB J -1 A_KillMaster
			Stop
		FastDeath:
			WSCB A 5 A_Scream
			Goto XDeath+3
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WSCB D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WSCB D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WSCB D 3 A_SetTranslucent(0.9,0)
			WSCB D 0 A_Scream
			WSCB D 3 A_SetTranslucent(0.8,0)
			WSCB D 0 A_NoBlocking
			WSCB D 3 A_SetTranslucent(0.7,0)
			WSCB D 3 A_SetTranslucent(0.6,0)
			WSCB D 3 A_SetTranslucent(0.5,0)
			WSCB D 3 A_SetTranslucent(0.4,0)
			WSCB D 3 A_SetTranslucent(0.3,0)
			WSCB D 3 A_SetTranslucent(0.2,0)
			WSCB D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
	}
}

ACTOR MultiDoctorSchabbs : DoctorSchabbs
{
	+THRUSPECIES
}