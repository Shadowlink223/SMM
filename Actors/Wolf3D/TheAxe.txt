ACTOR TheAxe 
{
	Health 1400
	Radius 33
	Height 70
	Speed 12
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+BOSS
	+NORADIUSDMG
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+LOOKALLAROUND
	SeeSound ""
	PainSound ""
	DeathSound "axe/death"
	ActiveSound ""
	AttackSound "nazi/fire"
	Obituary "%o was shot by the Axe"
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	Dropitem "DefaultPinataPop"
	Dropitem "DefaultPinataPop"
	Dropitem "SODGoldKey"
	Decal BulletChip
	States 
	{
		Spawn:
			TAXE A 0
			TAXE A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			TAXE A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			TAXE A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TAXE A 1 A_Look
			Loop
		See:
			TAXE AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TAXE E 15 A_FaceTarget
			TAXE F 6 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_FaceTarget
			TAXE G 6 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_FaceTarget
			TAXE H 6 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_FaceTarget
			TAXE G 6 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_FaceTarget
			TAXE F 6 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TAXE E 6
			Goto See
		Death.DukeDamage:
		Death.MightyBoot:
		Death.DukeExplosive:
		Death.DukeIce:
		Death.DukePistol:
			TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SMMCVarCheck",2) == 1, "FastDeath")
			TAXE A 2
			TAXE A 35 A_Scream
			TAXE I 7 A_NoBlocking
			TAXE JK 7 
			TAXE L 8
			TAXE M 30
			TAXE M -1 A_KillMaster
			Stop
		FastDeath:
			TAXE A 2 A_Scream
			Goto XDeath+4
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TAXE D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			TAXE D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TAXE D 3 A_SetTranslucent(0.9,0)
			TAXE D 0 A_Scream
			TAXE D 3 A_SetTranslucent(0.8,0)
			TAXE D 0 A_NoBlocking
			TAXE D 3 A_SetTranslucent(0.7,0)
			TAXE D 3 A_SetTranslucent(0.6,0)
			TAXE D 3 A_SetTranslucent(0.5,0)
			TAXE D 3 A_SetTranslucent(0.4,0)
			TAXE D 3 A_SetTranslucent(0.3,0)
			TAXE D 3 A_SetTranslucent(0.2,0)
			TAXE D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
	}
}

ACTOR MultiTheAxe : TheAxe
{
	+THRUSPECIES
}