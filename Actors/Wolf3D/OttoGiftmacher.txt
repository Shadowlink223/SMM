ACTOR OttoGiftmacher 
{
	Health 850
	Radius 26
	Height 69
	Speed 6
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+LOOKALLAROUND
	+BOSSDEATH
	SeeSound "otto/sight"
	PainSound ""
	DeathSound "otto/death"
	ActiveSound ""
	AttackSound "wolfen/missilefire"
	Obituary "%o was blown away by Otto Giftmacher's rocket."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
	Dropitem "WolfGoldKey"
	Decal Scorch
	States 
	{
		Spawn:
			WOTT A 1 A_Look
			Loop
		See:
			WOTT AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WOTT EEEEE 3 A_FaceTarget
			WOTT F 5 Bright
			TNT1 A 0 A_CustomMissile("OttoWolfRocket",32,16,0,8)
			Goto See
		Pain:
			WOTT D 3
			WOTT D 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			WOTT D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			WOTT D 16 A_Pain
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(680,2) == 1, "FastDeath")
			WOTT A 70 A_Scream
			WOTT G 5
			WOTT H 5
			WOTT I 5 A_NoBlocking
			WOTT J -1 A_KillMaster
			Stop
		FastDeath:
			WOTT A 5 A_Scream
			Goto XDeath+3
		Death.ZorchPistol:
		Death.Zorch:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WOTT D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WOTT D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WOTT D 3 A_SetTranslucent(0.9,0)
			WOTT D 0 A_Scream
			WOTT D 3 A_SetTranslucent(0.8,0)
			WOTT D 0 A_NoBlocking
			WOTT D 3 A_SetTranslucent(0.7,0)
			WOTT D 3 A_SetTranslucent(0.6,0)
			WOTT D 3 A_SetTranslucent(0.5,0)
			WOTT D 3 A_SetTranslucent(0.4,0)
			WOTT D 3 A_SetTranslucent(0.3,0)
			WOTT D 3 A_SetTranslucent(0.2,0)
			WOTT D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("OttoFrozen")
			WOTT K 0 A_GenericFreezeDeath
			WOTT K 70 A_UnsetSolid
			WOTT K 0 A_IceGuyDie
			WOTT K 1 A_IceGuyDie
			TNT1 A -1 A_BossDeath
			stop
		Raise:
			WOTT JIHG 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			WOTT A 3 A_SetTranslucent(0.2,0)
			WOTT A 3 A_SetTranslucent(0.3,0)
			WOTT A 3 A_SetTranslucent(0.4,0)
			WOTT A 3 A_SetTranslucent(0.5,0)
			WOTT A 3 A_SetTranslucent(0.6,0)
			WOTT A 3 A_SetTranslucent(0.7,0)
			WOTT A 3 A_SetTranslucent(0.8,0)
			WOTT A 3 A_SetTranslucent(0.9,0)
			WOTT A 3 A_SetTranslucent(1,0)
			WOTT A 1 A_SetShootable
			Goto See
	}
}

ACTOR OttoGiftmacherDoom : OttoGiftMacher
{
	Height 63
	Scale 1.0
}