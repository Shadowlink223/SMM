ACTOR Robot 
{
	Health 1600
	Radius 33
	Height 70
	Speed 8
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+LOOKALLAROUND
	SeeSound "robot/sight"
	PainSound ""
	DeathSound "robot/death"
	ActiveSound ""
	AttackSound "nazi/fire"
	Dropitem "DefaultPinataPop"
	Dropitem "DefaultPinataPop"
	Obituary "%o was shot by the Robot."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "WolfEnemyBullet", 0.0
	Dropitem "SODGoldKey"
	Decal BulletChip
	States 
	{
		Spawn:
			ROBO A 0
			ROBO A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			ROBO A 1 ACS_NamedExecuteAlways("SMMSpawningScript",0,1,0)
			ROBO A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			ROBO A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TNT1 A 0 A_GiveInventory("DukeBossSee",1)
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
		See2:
			ROBO AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			ROBO EEEEE 3 A_FaceTarget
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			ROBO F 5 Bright A_CustomMissile("KnightWolfRocket",58,10,0,8)
			TNT1 A 0 A_PlaySound("deathknight/missile", CHAN_WEAPON)
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			ROBO H 5 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			ROBO G 5 Bright A_CustomMissile("KnightWolfRocket",58,-10,0,8)
			TNT1 A 0 A_PlaySound("deathknight/missile", CHAN_WEAPON)
			TNT1 A 0 A_WolfAttack(0,"",0.666,64,32,2,4,160.0)
			ROBO H 5 Bright A_WolfAttack(0,"nazi/fire",0.666,64,32,2,4,160.0)
			Goto See
		Death.DukeDamage:
		Death.MightyBoot:
		Death.DukeExplosive:
		Death.DukeIce:
		Death.DukePistol:
			TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SMMCVarCheck",2) == 1, "FastDeath")
			ROBO A 2
			ROBO A 50 A_Scream
			ROBO I 5
			ROBO J 5 A_NoBlocking
			ROBO KLMN 5
			ROBO O 30
			ROBO O -1 A_KillMaster
			Stop
		FastDeath:
			ROBO A 2 A_Scream
			Goto XDeath+4
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			ROBO D 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			ROBO D 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			ROBO D 3 A_SetTranslucent(0.9,0)
			ROBO D 0 A_Scream
			ROBO D 3 A_SetTranslucent(0.8,0)
			ROBO D 0 A_NoBlocking
			ROBO D 3 A_SetTranslucent(0.7,0)
			ROBO D 3 A_SetTranslucent(0.6,0)
			ROBO D 3 A_SetTranslucent(0.5,0)
			ROBO D 3 A_SetTranslucent(0.4,0)
			ROBO D 3 A_SetTranslucent(0.3,0)
			ROBO D 3 A_SetTranslucent(0.2,0)
			ROBO D 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop
	}
}

ACTOR MultiRobot : Robot
{
	+THRUSPECIES
}