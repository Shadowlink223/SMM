ACTOR WolfGuard 
{
	Health 25
	Radius 16
	Height 48
	Speed 6
	PainChance 200
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	SeeSound "guard/sight"
	PainSound ""
	DeathSound "nazi/death"
	ActiveSound ""
	AttackSound "nazi/fire"
	Dropitem "DefaultPinataPop"
	Obituary "%o was shot by a nazi guard."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Dropitem "WolfClip"
	Decal BulletChip
	States 
	{
		Spawn:
			WGRD A 1 A_Look
			Loop
		See:
			WGRD BBBCCDDDEE 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag(NOPAIN,0)
			WGRD GH 10 A_FaceTarget
			WGRD I 0 A_PlaySound("nazi/fire", CHAN_WEAPON)
			TNT1 A 0 A_JumpIfCloser (128, "Bullet1")
			TNT1 A 0 A_JumpIfCloser (256, "Bullet2")
			TNT1 A 0 A_JumpIfCloser (384, "Bullet3")
			TNT1 A 0 A_JumpIfCloser (512, "Bullet4")
			TNT1 A 0 A_JumpIfCloser (640, "Bullet5")
			TNT1 A 0 A_JumpIfCloser (768, "Bullet6")
			Goto Bullet7
		Bullet1:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD1",32,0,random(-6,6),8)
			Goto MissileEnd
		Bullet2:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD2",32,0,random(-7,7),8)
			Goto MissileEnd
		Bullet3:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD3",32,0,random(-8,8),8)
			Goto MissileEnd
		Bullet4:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD4",32,0,random(-9,9),8)
			Goto MissileEnd
		Bullet5:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD5",32,0,random(-10,10),8)
			Goto MissileEnd
		Bullet6:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD6",32,0,random(-11,11),8)
			Goto MissileEnd
		Bullet7:
			WGRD I 5 Bright A_CustomMissile("WolfEnemyBulletD7",32,0,random(-12,12),8)
			Goto MissileEnd
		MissileEnd:
			WGRD I 5
			Goto See
		Pain:
			WGRD F 3
			WGRD F 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			WGRD F 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			WGRD F 16 A_Pain
			Goto See
		Pain.MarathonStun:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_GiveInventory("NothingSpeed")
			TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
			WGRD F 5 A_FaceTarget
			TNT1 A 0 A_Gravity
			WGRD F 30 A_Pain
			WGRD F 70
			Goto See
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		DukeGibStart:
			TNT1 A 0 A_UnsetSolid
			TNT1 A 0 A_SpawnDebris("DukeGibs1")
			TNT1 A 0 A_SpawnDebris("DukeGibs2")
			TNT1 A 0 A_SpawnDebris("DukeGibs3")
			TNT1 A 0 A_SpawnDebris("DukeGibs4")
			TNT1 A 0 A_SpawnDebris("DukeGibs5")
			TNT1 A 0 A_SpawnDebris("DukeGibs6")
			TNT1 A 0 A_PlaySound("duke/enemygib")
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
		Death:
		XDeath:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
			WGRD K 7
			WGRD L 7 A_Scream
			WGRD M 8 A_NoBlocking
			WGRD N -1
			Stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			WGRD F 0 A_Stop
			TNT1 A 0 A_ChangeFlag("DONTGIB",1)
			TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			WGRD F 3 A_SpawnItem("ZorchEffectSmall")
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			WGRD F 3 A_SetTranslucent(0.9,0)
			WGRD F 0 A_Scream
			WGRD F 3 A_SetTranslucent(0.8,0)
			WGRD F 0 A_NoBlocking
			WGRD F 3 A_SetTranslucent(0.7,0)
			WGRD F 3 A_SetTranslucent(0.6,0)
			WGRD F 3 A_SetTranslucent(0.5,0)
			WGRD F 3 A_SetTranslucent(0.4,0)
			WGRD F 3 A_SetTranslucent(0.3,0)
			WGRD F 3 A_SetTranslucent(0.2,0)
			WGRD F 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1
			stop
		Death.DukeIce:
			TNT1 A 0 A_PlaySound("duke/enemyfreeze")
			TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
			TNT1 A 0 A_Jump(218,"Ice")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.Ice:
			TNT1 A 0 A_GiveToTarget("KillCount",1)
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_NoBlocking
			//TNT1 A 0 A_SpawnItem("GuardFrozen")
			WGRD O 0 A_GenericFreezeDeath
			WGRD O 70 A_UnsetSolid
			WGRD O 0 A_IceGuyDie
			WGRD O 1 A_IceGuyDie
			TNT1 A -1
			stop
		Raise:
			WGRD NMLK 5
			Goto See
		ZorchRaise:
			TNT1 A 1 A_UnsetShootable
			TNT1 A 1 A_SpawnItem("SpawnFire2")
			TNT1 A 0 A_PlaySound("archvile/resummon")
			TNT1 A 1 A_TakeInventory("EnemyZorched",1)
			TNT1 A 0 A_SetTranslucent(0.1,0)
			WGRD A 3 A_SetTranslucent(0.2,0)
			WGRD A 3 A_SetTranslucent(0.3,0)
			WGRD A 3 A_SetTranslucent(0.4,0)
			WGRD A 3 A_SetTranslucent(0.5,0)
			WGRD A 3 A_SetTranslucent(0.6,0)
			WGRD A 3 A_SetTranslucent(0.7,0)
			WGRD A 3 A_SetTranslucent(0.8,0)
			WGRD A 3 A_SetTranslucent(0.9,0)
			WGRD A 3 A_SetTranslucent(1,0)
			WGRD A 1 A_SetShootable
			Goto See
		DeathHappyfun:
			TNT1 A 0
			TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
			TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
			TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
			Goto XDeath+2
		DeathBrutal:
			TNT1 A 0
			TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
			TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
			TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160)
			Goto XDeath+2
	}
}

ACTOR MultiWolfGuard : WolfGuard
{
	+THRUSPECIES
}